{-

This file contains:
- ...

-}
{-# OPTIONS --safe #-}
module Classics.Topology.Properties where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Data.Sigma
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.Relation.Nullary

open import Classics.Axioms.ExcludedMiddle
open import Classics.Foundations.Powerset

open import Classics.Topology.Base

private
  variable
    â„“ : Level


module TopologyProperties (decide : LEM) where

  open Powerset decide
  open Topology decide
  open TopologicalSpace

  module _
    (X : TopologicalSpace â„“) where

    -- Some convenient renamings

    Subset : Type _
    Subset = â„™ (X .set)

    Open : â„™ Subset
    Open = X .openset

    Closed : â„™ Subset
    Closed A = Open (âˆ A)

    isOpenSubSet : Subset â†’ Type _
    isOpenSubSet U = U âˆˆ Open

    isClosedSubSet : Subset â†’ Type _
    isClosedSubSet A = âˆ A âˆˆ Open

    -- Open coverings

    _covers_ : â„™ Subset â†’ Subset â†’ Type _
    _covers_ ğ’° A = A âŠ† union ğ’° Ã— ğ’° âŠ† Open

    unionâˆˆOpen : {ğ’° : â„™ Subset} â†’ ğ’° âŠ† Open â†’ union ğ’° âˆˆ Open
    unionâˆˆOpen = X .âˆª-close


    -- Neighbourhood around a given point

    â„•bh : X .set â†’ â„™ Subset
    â„•bh x = rep x âˆ© Open

    Nâˆˆâ„•bhxâ†’xâˆˆN : {x : X .set}{N : Subset} â†’ N âˆˆ â„•bh x â†’ x âˆˆ N
    Nâˆˆâ„•bhxâ†’xâˆˆN {x = x} {N = N} Nâˆˆâ„•x = Aâˆˆrepxâ†’xâˆˆA {A = N} (leftâˆˆ-âˆ© (rep x) Open Nâˆˆâ„•x)

    Nâˆˆâ„•bhxâ†’NâˆˆOpen : {x : X .set}{N : Subset} â†’ N âˆˆ â„•bh x â†’ N âˆˆ Open
    Nâˆˆâ„•bhxâ†’NâˆˆOpen {x = x} {N = N} = rightâˆˆ-âˆ© (rep x) Open

    getâ„•bh : {x : X .set}{N : Subset} â†’ x âˆˆ N â†’ N âˆˆ Open â†’ N âˆˆ â„•bh x
    getâ„•bh {x = x} {N = N} xâˆˆN NâˆˆOpen = âˆˆâ†’âˆˆâˆ© (rep x) Open (xâˆˆAâ†’Aâˆˆrepx {A = N} xâˆˆN) NâˆˆOpen

    totalâˆˆâ„•bh : {x : X .set} â†’ total âˆˆ â„•bh x
    totalâˆˆâ„•bh {x = x} = getâ„•bh {x = x} (xâˆˆtotal {x = x}) (X .has-total)

    â„•bhâˆ© : {x : X .set}{U V : Subset} â†’ U âˆˆ â„•bh x â†’ V âˆˆ â„•bh x â†’ U âˆ© V âˆˆ â„•bh x
    â„•bhâˆ© {U = U} {V = V} Uâˆˆâ„•x Vâˆˆâ„•x =
      getâ„•bh (âˆ©-âˆˆrep U V (Nâˆˆâ„•bhxâ†’xâˆˆN Uâˆˆâ„•x) (Nâˆˆâ„•bhxâ†’xâˆˆN Vâˆˆâ„•x))
        (X .âˆ©-close (Nâˆˆâ„•bhxâ†’NâˆˆOpen Uâˆˆâ„•x) (Nâˆˆâ„•bhxâ†’NâˆˆOpen Vâˆˆâ„•x))


    -- Inside interior of some someset

    _Î£âˆˆâˆ˜_ : (x : X .set) â†’ (U : Subset) â†’ Type _
    x Î£âˆˆâˆ˜ U = Î£[ N âˆˆ Subset ] (N âˆˆ â„•bh x) Ã— N âŠ† U

    -- It reads "the point x is inside the interior of U"
    -- or "x is an interior point of U".
    _âˆˆâˆ˜_ : (x : X .set) â†’ (U : Subset) â†’ Type _
    x âˆˆâˆ˜ U = âˆ¥ x Î£âˆˆâˆ˜ U âˆ¥

    âˆˆâ†’âˆˆâˆ˜ : {x : X .set}{U : Subset} â†’ U âˆˆ Open â†’ x âˆˆ U â†’ x âˆˆâˆ˜ U
    âˆˆâ†’âˆˆâˆ˜ {U = U} UâˆˆOpen xâˆˆU = âˆ£ _ , getâ„•bh xâˆˆU UâˆˆOpen , âŠ†-refl {A = U} âˆ£

    -- If every point of a subset U is its interior point,
    -- the subset U is open subset.
    â„•bhCriterionOfOpenness : {U : Subset} â†’ ((x : X .set) â†’ x âˆˆ U â†’ x âˆˆâˆ˜ U) â†’ U âˆˆ Open
    â„•bhCriterionOfOpenness {U = U} p = UâˆˆOpen
      where
      P : Subset â†’ hProp _
      P N = âˆ¥ Î£[ x âˆˆ X .set ] (N âˆˆ â„•bh x) Ã— N âŠ† U âˆ¥ , squash

      ğ’° : â„™ Subset
      ğ’° = specify P

      rec-helper1 : {N : Subset} â†’ âˆ¥ Î£[ x âˆˆ X .set ] (N âˆˆ â„•bh x) Ã— N âŠ† U âˆ¥ â†’ N âˆˆ Open
      rec-helper1 = Prop.rec (isPropâˆˆ {A = Open}) Î» (_ , p , _) â†’ Nâˆˆâ„•bhxâ†’NâˆˆOpen p

      ğ’°âŠ†Open : ğ’° âŠ† Open
      ğ’°âŠ†Open p = rec-helper1 (âˆˆâ†’Inhab P p)

      ğ•Œ : Subset
      ğ•Œ = union ğ’°

      ğ•ŒâˆˆOpen : ğ•Œ âˆˆ Open
      ğ•ŒâˆˆOpen = X .âˆª-close ğ’°âŠ†Open

      rec-helper2 : {N : Subset} â†’ âˆ¥ Î£[ x âˆˆ X .set ] (N âˆˆ â„•bh x) Ã— N âŠ† U âˆ¥ â†’ N âŠ† U
      rec-helper2 = Prop.rec isPropâŠ† Î» (_ , _ , p) â†’ p

      Nâˆˆğ’°â†’NâŠ†U : (N : Subset) â†’ N âˆˆ ğ’° â†’ N âŠ† U
      Nâˆˆğ’°â†’NâŠ†U _ p = rec-helper2 (âˆˆâ†’Inhab P p)

      ğ•ŒâŠ†U : ğ•Œ âŠ† U
      ğ•ŒâŠ†U = unionâŠ† Nâˆˆğ’°â†’NâŠ†U

      UâŠ†ğ•Œ : U âŠ† ğ•Œ
      UâŠ†ğ•Œ xâˆˆU = âˆˆunion
        (Prop.map (Î» (N , Nâˆˆâ„•x , NâŠ†U) â†’ N , Nâˆˆâ„•bhxâ†’xâˆˆN Nâˆˆâ„•x , Inhabâ†’âˆˆ P âˆ£ _ , Nâˆˆâ„•x , NâŠ†U âˆ£) (p _ xâˆˆU))

      ğ•Œâ‰¡U : ğ•Œ â‰¡ U
      ğ•Œâ‰¡U = biâŠ†â†’â‰¡ ğ•ŒâŠ†U UâŠ†ğ•Œ

      UâˆˆOpen : U âˆˆ Open
      UâˆˆOpen = subst (_âˆˆ Open) ğ•Œâ‰¡U ğ•ŒâˆˆOpen


    -- Separating a point from a subset using open sets

    Î£Sep : (x : X .set) â†’ Subset â†’ Type _
    Î£Sep x A = Î£[ U âˆˆ Subset ] (U âˆˆ â„•bh x) Ã— (A âˆ© U â‰¡ âˆ…)

    Î£SepâŠ† : {x : X .set}{A B : Subset} â†’ A âŠ† B â†’ Î£Sep x B â†’ Î£Sep x A
    Î£SepâŠ† {A = A} {B = B} AâŠ†B (U , Uâˆˆâ„•x , Bâˆ©Uâ‰¡âˆ…) = U , Uâˆˆâ„•x , AâŠ†B+Bâˆ©Câ‰¡âˆ…â†’Aâˆ©Câ‰¡âˆ… AâŠ†B Bâˆ©Uâ‰¡âˆ…

    -- It reads as "there merely exists a neighbourhood of x that is separated from A".
    Sep : (x : X .set) â†’ Subset â†’ Type _
    Sep x A = âˆ¥ Î£Sep x A âˆ¥

    SepâŠ† : {x : X .set}{A B : Subset} â†’ A âŠ† B â†’ Sep x B â†’ Sep x A
    SepâŠ† AâŠ†B = Prop.map (Î£SepâŠ† AâŠ†B)

    Sepâ†’âˆˆâˆ˜âˆ : {x : X .set}{A : Subset} â†’ Sep x A â†’ x âˆˆâˆ˜ (âˆ A)
    Sepâ†’âˆˆâˆ˜âˆ = Prop.map (Î» (U , Uâˆˆâ„•x , Aâˆ©Uâ‰¡âˆ…) â†’ U , Uâˆˆâ„•x , Aâˆ©B=âˆ…â†’AâŠ†âˆB (âˆ©-Comm _ _ âˆ™ Aâˆ©Uâ‰¡âˆ…))


    -- It reads as "there merely exists neighbourhood of x and A respectively that don't intersect with each other",
    -- or "point x and subset A are separating by open sets"
    SepOpen : (x : X .set) â†’ Subset â†’ Type _
    SepOpen x A = âˆ¥ Î£[ U âˆˆ Subset ] Î£[ V âˆˆ Subset ] (U âˆˆ Open) Ã— A âŠ† U Ã— (V âˆˆ â„•bh x) Ã— (A âˆ© V â‰¡ âˆ…) âˆ¥

    SepOpenâŠ† : {x : X .set}{A U : Subset} â†’ U âˆˆ Open â†’ A âŠ† U â†’ Sep x U â†’ SepOpen x A
    SepOpenâŠ† {U = U} UâˆˆOpen AâŠ†U =
      Prop.map (Î» (V , Vâˆˆâ„•x , Uâˆ©Vâ‰¡âˆ…) â†’ U , V , UâˆˆOpen , AâŠ†U , Vâˆˆâ„•x , AâŠ†B+Bâˆ©Câ‰¡âˆ…â†’Aâˆ©Câ‰¡âˆ… AâŠ†U Uâˆ©Vâ‰¡âˆ…)

    SepOpenâ†’Sep : {x : X .set}{A : Subset} â†’ SepOpen x A â†’ Sep x A
    SepOpenâ†’Sep = Prop.map (Î» (_ , V , _ , _ , Vâˆˆâ„•x , Aâˆ©Vâ‰¡âˆ…) â†’ V , Vâˆˆâ„•x , Aâˆ©Vâ‰¡âˆ…)

    -- A subset K âŠ† X is closed if for any x âˆ‰ K, there merely exists neigubourhood of x separating from K.
    SepCriterionOfClosedness : {K : Subset} â†’ ((x : X .set) â†’ x âˆ‰ K â†’ Sep x K) â†’ K âˆˆ Closed
    SepCriterionOfClosedness {K = K} sep = â„•bhCriterionOfOpenness xâˆ‰Kâ†’xâˆˆâˆ˜âˆK
      where
      xâˆ‰Kâ†’xâˆˆâˆ˜âˆK : (x : X .set) â†’ x âˆˆ (âˆ K) â†’ x âˆˆâˆ˜ (âˆ K)
      xâˆ‰Kâ†’xâˆˆâˆ˜âˆK x xâˆˆâˆK = Sepâ†’âˆˆâˆ˜âˆ (sep x (âˆˆâˆâ†’âˆ‰ {A = K} xâˆˆâˆK))


    -- Given a finite covering ğ’°
    -- such that for any open U âˆˆ ğ’°, there merely exists a neighbourhood of x not intersecting with U,
    -- then there merely exists a neighbourhood of x that is not intersecting with the union of opens in ğ’°.
    unionSep : (x : X .set)
      (ğ’° : â„™ Subset)(ğ’°âŠ†Open : ğ’° âŠ† Open)
      (sep : (U : Subset) â†’ U âˆˆ ğ’° â†’ Sep x U)
      â†’ isFinSubset ğ’° â†’ Sep x (union ğ’°)
    unionSep x ğ’° _ _ isfinâˆ… = âˆ£ total , totalâˆˆâ„•bh {x = x} , âˆ©-rUnit (union ğ’°) âˆ™ unionâˆ… âˆ£
    unionSep x ğ’° ğ’°âŠ†Open sep (isfinsuc U {A = ğ’°â‚€} finğ’°â‚€) = subst (Sep x) (sym unionâˆª[A]) sepğ•Œâ‚€âˆªU
      where
      ğ•Œâ‚€ : Subset
      ğ•Œâ‚€ = union ğ’°â‚€

      ğ’°â‚€âŠ†ğ’° : ğ’°â‚€ âŠ† ğ’°
      ğ’°â‚€âŠ†ğ’° = âˆª-leftâŠ† ğ’°â‚€ [ U ]

      ğ’°â‚€âŠ†Open : ğ’°â‚€ âŠ† Open
      ğ’°â‚€âŠ†Open = âŠ†-trans {A = ğ’°â‚€} ğ’°â‚€âŠ†ğ’° ğ’°âŠ†Open

      ğ•Œâ‚€âˆˆOpen : ğ•Œâ‚€ âˆˆ Open
      ğ•Œâ‚€âˆˆOpen = unionâˆˆOpen ğ’°â‚€âŠ†Open

      -- TODO : Make a solver to deal with these problems.
      âˆªâˆ…-helper : {A B C D : Subset} â†’ A âˆ© C â‰¡ âˆ… â†’ B âˆ© D â‰¡ âˆ… â†’ (A âˆª B) âˆ© (C âˆ© D) â‰¡ âˆ…
      âˆªâˆ…-helper {A = A} {B = B} {C = C} {D = D} Aâˆ©Câ‰¡âˆ… Bâˆ©Dâ‰¡âˆ… =
          âˆ©-âˆª-lDist _ _ _
        âˆ™ (Î» i â†’ âˆ©-Assoc A C D i âˆª (B âˆ© âˆ©-Comm C D i))
        âˆ™ (Î» i â†’ ((A âˆ© C) âˆ© D) âˆª âˆ©-Assoc B D C i)
        âˆ™ (Î» i â†’ (Aâˆ©Câ‰¡âˆ… i âˆ© D) âˆª (Bâˆ©Dâ‰¡âˆ… i âˆ© C))
        âˆ™ (Î» i â†’ âˆ©-lZero D i âˆª âˆ©-lZero C i)
        âˆ™ âˆª-Idem _

      ind-Sep-helper : (A B : Subset) â†’ A âˆˆ Open â†’ B âˆˆ Open â†’ Î£Sep x A â†’ Î£Sep x B â†’ Î£Sep x (A âˆª B)
      ind-Sep-helper _ _ _ _ (VA , VAâˆˆNx , VAâˆ…) (VB , VBâˆˆNx , VBâˆ…) =
        VA âˆ© VB , â„•bhâˆ© VAâˆˆNx VBâˆˆNx , âˆªâˆ…-helper VAâˆ… VBâˆ…

      ind-Sep : (A B : Subset) â†’ A âˆˆ Open â†’ B âˆˆ Open â†’ _
      ind-Sep A B p q = Prop.map2 (ind-Sep-helper A B p q)

      sepğ•Œâ‚€ : Sep x ğ•Œâ‚€
      sepğ•Œâ‚€ = unionSep _ _ ğ’°â‚€âŠ†Open (Î» U Uâˆˆğ’°â‚€ â†’ sep U (âˆˆâŠ†-trans {A = ğ’°â‚€} Uâˆˆğ’°â‚€ ğ’°â‚€âŠ†ğ’°)) finğ’°â‚€

      Uâˆˆğ’° : U âˆˆ ğ’°
      Uâˆˆğ’° = [A]âŠ†Sâ†’AâˆˆS (âˆª-rightâŠ† ğ’°â‚€ [ U ])

      UâˆˆOpen : U âˆˆ Open
      UâˆˆOpen = âˆˆâŠ†-trans {A = ğ’°} Uâˆˆğ’° ğ’°âŠ†Open

      sep[U] : Sep x U
      sep[U] = sep U Uâˆˆğ’°

      sepğ•Œâ‚€âˆªU : Sep x (ğ•Œâ‚€ âˆª U)
      sepğ•Œâ‚€âˆªU = ind-Sep _ _ ğ•Œâ‚€âˆˆOpen UâˆˆOpen sepğ•Œâ‚€ sep[U]


    -- Compactness

    isCompactSubset : Subset â†’ Type _
    isCompactSubset K =
      {ğ’° : â„™ Subset} â†’ ğ’° covers K â†’ âˆ¥ Î£[ ğ’°â‚€ âˆˆ â„™ Subset ] ğ’°â‚€ âŠ† ğ’° Ã— isFinSubset ğ’°â‚€ Ã— ğ’°â‚€ covers K âˆ¥

    isCompact : Type _
    isCompact = isCompactSubset total


    {-

      Hausdorff Space

    -}

    -- Hausdorff Separation Axiom
    isHausdorff : Type _
    isHausdorff =
      {x y : X .set} â†’ Â¬ x â‰¡ y â†’ âˆ¥ Î£[ U âˆˆ Subset ] Î£[ V âˆˆ Subset ] (U âˆˆ â„•bh x) Ã— (V âˆˆ â„•bh y) Ã— (U âˆ© V â‰¡ âˆ…) âˆ¥

    module _
      (haus : isHausdorff) where

      -- In a Hausdorff space X,
      -- point x âˆˆ X and subset K âŠ† X are separating by open sets
      -- if x âˆ‰ K and K is compact.
      sepOpenCompact : {x : X .set}{K : Subset} â†’ isCompactSubset K â†’ x âˆ‰ K â†’ SepOpen x K
      sepOpenCompact {x = xâ‚€} {K = K} takefin xâ‚€âˆ‰K = sepOpen
        where
        P : Subset â†’ hProp _
        P U = âˆ¥ Î£[ x âˆˆ X .set ] (x âˆˆ K) Ã— (U âˆˆ â„•bh x) Ã— (Sep xâ‚€ U) âˆ¥ , squash

        ğ’° : â„™ Subset
        ğ’° = specify P

        ğ’°âŠ†Open : ğ’° âŠ† Open
        ğ’°âŠ†Open p =
          Prop.rec (isPropâˆˆ {A = Open}) (Î» (_ , _ , q , _) â†’ Nâˆˆâ„•bhxâ†’NâˆˆOpen q) (âˆˆâ†’Inhab P p)

        ğ•Œ : Subset
        ğ•Œ = union ğ’°

        -- A shuffle of propositions
        KâŠ†ğ•Œ : K âŠ† ğ•Œ
        KâŠ†ğ•Œ xâˆˆK =
          Prop.rec (isPropâˆˆ {A = ğ•Œ})
          (Î» (U , V , Uâˆˆâ„•x , Vâˆˆâ„•xâ‚€ , Uâˆ©Vâ‰¡âˆ…) â†’
             âˆˆunion âˆ£ U , Nâˆˆâ„•bhxâ†’xâˆˆN Uâˆˆâ„•x , Inhabâ†’âˆˆ P âˆ£ _ , xâˆˆK , Uâˆˆâ„•x , âˆ£ V , Vâˆˆâ„•xâ‚€ , Uâˆ©Vâ‰¡âˆ… âˆ£ âˆ£ âˆ£)
          (haus (âˆˆâˆ‰â†’â‰¢ xâˆˆK xâ‚€âˆ‰K))

        ğ’°-covers-K : ğ’° covers K
        ğ’°-covers-K = KâŠ†ğ•Œ , ğ’°âŠ†Open

        ğ•ŒâˆˆOpen : ğ•Œ âˆˆ Open
        ğ•ŒâˆˆOpen = unionâˆˆOpen ğ’°âŠ†Open

        -- Another shuffle of propositions
        âˆƒğ’°â‚€ : âˆ¥ Î£[ ğ’°â‚€ âˆˆ â„™ Subset ] ğ’°â‚€ âŠ† Open Ã— isFinSubset ğ’°â‚€ Ã— ğ’°â‚€ covers K Ã— ((U : Subset) â†’ U âˆˆ ğ’°â‚€ â†’ Sep xâ‚€ U) âˆ¥
        âˆƒğ’°â‚€ =
          Prop.map
          (Î» (ğ’°â‚€ , ğ’°â‚€âŠ†ğ’° , finğ’°â‚€ , ğ’°â‚€covK) â†’
              ğ’°â‚€ , âŠ†-trans {C = Open} ğ’°â‚€âŠ†ğ’° ğ’°âŠ†Open , finğ’°â‚€ , ğ’°â‚€covK ,
              Î» U Uâˆˆğ’°â‚€ â†’ Prop.rec squash (Î» (_ , _ , _ , sep) â†’ sep) (âˆˆâ†’Inhab P (âˆˆâŠ†-trans {B = ğ’°} Uâˆˆğ’°â‚€ ğ’°â‚€âŠ†ğ’°)))
          (takefin ğ’°-covers-K)

        sepOpen : SepOpen xâ‚€ K
        sepOpen = Prop.rec squash
          (Î» (_ , ğ’°â‚€âŠ†Open , finâŠ†ğ’°â‚€ , ğ’°â‚€covK , sep)
              â†’  SepOpenâŠ† (unionâˆˆOpen ğ’°â‚€âŠ†Open) (ğ’°â‚€covK .fst) (unionSep _ _ ğ’°â‚€âŠ†Open sep finâŠ†ğ’°â‚€)) âˆƒğ’°â‚€

      -- Compact subset of Hausdorff space is closed subset.
      isCompactSubsetâ†’isClosedSubSet : {K : Subset} â†’ isCompactSubset K â†’ isClosedSubSet K
      isCompactSubsetâ†’isClosedSubSet takefin = SepCriterionOfClosedness (Î» _ xâˆ‰K â†’ SepOpenâ†’Sep (sepOpenCompact takefin xâˆ‰K))
