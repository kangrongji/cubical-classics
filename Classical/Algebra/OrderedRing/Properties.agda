{-

  Properties of ordered ring

-}
{-# OPTIONS --safe --experimental-lossy-unification #-}
module Classical.Algebra.OrderedRing.Properties where

open import Cubical.Foundations.Prelude

open import Cubical.Data.Unit
open import Cubical.Data.Empty as Empty
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.Data.Nat using (â„• ; zero ; suc)

open import Cubical.Algebra.Ring
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRingSolver.Reflection
open import Cubical.Relation.Nullary

open import Classical.Algebra.OrderedRing.Base

private
  variable
    â„“ â„“' : Level


private
  module Helpers {â„“ : Level}(ð“¡ : CommRing â„“) where
    open CommRingStr (ð“¡ .snd)

    helper1 : (x : ð“¡ .fst) â†’ x â‰¡ (x - 0r)
    helper1 = solve ð“¡

    helper2 : (x y : ð“¡ .fst) â†’ x - y â‰¡ - (y - x)
    helper2 = solve ð“¡

    helper3 : (x y z : ð“¡ .fst) â†’ (z - y) + (y - x) â‰¡ z - x
    helper3 = solve ð“¡

    helper4 : (x y : ð“¡ .fst) â†’ (y - x) + x â‰¡ y
    helper4 = solve ð“¡

    helper5 : (x y z w : ð“¡ .fst) â†’ (y - x) + (w - z) â‰¡ (y + w) - (x + z)
    helper5 = solve ð“¡

    helper6 : (x y : ð“¡ .fst) â†’ y - x â‰¡ ((- x) - (- y))
    helper6 = solve ð“¡

    helper7 : (x y : ð“¡ .fst) â†’ x Â· (- y) â‰¡ - (x Â· y)
    helper7 = solve ð“¡

    helper8 : (x y : ð“¡ .fst) â†’ (- x) Â· (- y) â‰¡ x Â· y
    helper8 = solve ð“¡

    helper9 : (x y z : ð“¡ .fst) â†’ (x - 0r) Â· (z - y) â‰¡ (x Â· z) - (x Â· y)
    helper9 = solve ð“¡

    helper10 : (x y z : ð“¡ .fst) â†’ (z - y) Â· (x - 0r) â‰¡ (z Â· x) - (y Â· x)
    helper10 = solve ð“¡

    helper11 : (x y : ð“¡ .fst) â†’ - ((- x) Â· y) â‰¡ x Â· y
    helper11 = solve ð“¡

    helper12 : (x y z : ð“¡ .fst) â†’ y - x â‰¡ (y + z) - (x + z)
    helper12 = solve ð“¡

    helper13 : (x y z : ð“¡ .fst) â†’ y - x â‰¡ (z + y) - (z + x)
    helper13 = solve ð“¡

    helper14 : (x y : ð“¡ .fst) â†’ x â‰¡ (y + x) - y
    helper14 = solve ð“¡

    helper15 : (x y : ð“¡ .fst) â†’ (x - 0r) Â· (y - 1r) â‰¡ (x Â· y) - x
    helper15 = solve ð“¡

    helper16 : 1r - 0r â‰¡ 1r
    helper16 = solve ð“¡

    helper17 : (n q : ð“¡ .fst) â†’ (1r + n) Â· q â‰¡ (n Â· q) + q
    helper17 = solve ð“¡

    helper18 : (x : ð“¡ .fst) â†’ - (1r + x) â‰¡ - 1r - x
    helper18 = solve ð“¡

    helper19 : (x y : ð“¡ .fst) â†’ (x - y) + y â‰¡ x
    helper19 = solve ð“¡


module OrderedRingStr (ð“¡â‚’ : OrderedRing â„“ â„“') where

  private
    ð“¡ = ð“¡â‚’ .fst
    R = ð“¡â‚’ .fst .fst

  open RingTheory (CommRingâ†’Ring ð“¡)
  open CommRingStr   (ð“¡â‚’ .fst .snd)
  open OrderStrOnCommRing (ð“¡â‚’ .snd)

  open Helpers ð“¡


  private
    isSetR = is-set

    variable
      x y z w : R


  {-

    Strict Ordering

  -}

  _>_ : R â†’ R â†’ Type â„“'
  x > y = (x - y) >0

  _<_ : R â†’ R â†’ Type â„“'
  x < y = y > x

  infix 4 _>_ _<_

  isProp< : {x y : R} â†’ isProp (x < y)
  isProp< {x = x} {y = y} = isProp>0 (y - x)

  >0â‰¡>0r : (x : R) â†’ (x >0) â‰¡ (x > 0r)
  >0â‰¡>0r x i = (helper1 x i) >0


  <-asym : x < y â†’ x > y â†’ âŠ¥
  <-asym {x = x} {y = y} x<y x>y = >0-asym (y - x) x<y (subst (_>0) (helper2 x y) x>y)

  <-arefl : x < y â†’ x â‰¡ y â†’ âŠ¥
  <-arefl {x = x} {y = y} x<y xâ‰¡y = <-asym {x = x} {y = y} x<y (transport (Î» i â†’ xâ‰¡y i < xâ‰¡y (~ i)) x<y)

  >-arefl : x > y â†’ x â‰¡ y â†’ âŠ¥
  >-arefl x>y xâ‰¡y = <-arefl x>y (sym xâ‰¡y)

  <-trans : x < y â†’ y < z â†’ x < z
  <-trans {x = x} {y = y} {z = z} x<y y<z = subst (_>0) (helper3 x y z) (>0-+ (z - y) (y - x) y<z x<y)


  data Trichotomy (x y : R) : Type (â„“-max â„“ â„“') where
    lt : x < y â†’ Trichotomy x y
    eq : x â‰¡ y â†’ Trichotomy x y
    gt : x > y â†’ Trichotomy x y

  isPropTrichotomy : (x y : R) â†’ isProp (Trichotomy x y)
  isPropTrichotomy x y (lt x<y) (lt x<y') i = lt (isProp< x<y x<y' i)
  isPropTrichotomy x y (eq xâ‰¡y) (eq xâ‰¡y') i = eq (isSetR _ _ xâ‰¡y xâ‰¡y' i)
  isPropTrichotomy x y (gt x>y) (gt x>y') i = gt (isProp< x>y x>y' i)
  isPropTrichotomy x y (lt x<y) (eq xâ‰¡y) = Empty.rec (<-arefl x<y xâ‰¡y)
  isPropTrichotomy x y (lt x<y) (gt x>y) = Empty.rec (<-asym x<y x>y)
  isPropTrichotomy x y (gt x>y) (eq xâ‰¡y) = Empty.rec (<-arefl x>y (sym xâ‰¡y))
  isPropTrichotomy x y (gt x>y) (lt x<y) = Empty.rec (<-asym x<y x>y)
  isPropTrichotomy x y (eq xâ‰¡y) (lt x<y) = Empty.rec (<-arefl x<y xâ‰¡y)
  isPropTrichotomy x y (eq xâ‰¡y) (gt x>y) = Empty.rec (<-arefl x>y (sym xâ‰¡y))

  trichotomy : (x y : R) â†’ Trichotomy x y
  trichotomy x y with trichotomy>0 (y - x)
  ... | lt x<y = gt (subst (_>0) (sym (helper2 x y)) x<y)
  ... | eq xâ‰¡y = eq (sym (+Lid _) âˆ™ (Î» i â†’ xâ‰¡y (~ i) + x) âˆ™ helper4 x y)
  ... | gt x>y = lt x>y

  dec< : (x y : R) â†’ Dec (x < y)
  dec< x y with trichotomy x y
  ... | lt x<y = yes x<y
  ... | eq xâ‰¡y = no (Î» p â†’ <-arefl p xâ‰¡y)
  ... | gt x>y = no (Î» p â†’ <-asym  p x>y)


  +-Pres< : x < y â†’ z < w â†’ x + z < y + w
  +-Pres< x<y z<w = subst (_>0) (helper5 _ _ _ _) (>0-+ _ _ x<y z<w)

  +-lPres< : x < y â†’ z + x < z + y
  +-lPres< {z = z} x<y = subst (_>0) (helper13 _ _ z) x<y

  +-rPres< : x < y â†’ x + z < y + z
  +-rPres< {z = z} x<y = subst (_>0) (helper12 _ _ z) x<y

  -Reverse< : x < y â†’ - x > - y
  -Reverse< x<y = subst (_>0) (helper6 _ _) x<y


  +-rPosâ†’> : x > 0r â†’ y + x > y
  +-rPosâ†’> {x = x} {y = y} x>0 = subst (y + x >_) (+Rid y) (+-lPres< {z = y} x>0)

  +-rNegâ†’< : x < 0r â†’ y + x < y
  +-rNegâ†’< {x = x} {y = y} x<0 = subst (_> y + x) (+Rid y) (+-lPres< {z = y} x<0)


  Â·-lPosPres< : x > 0r â†’ y < z â†’ x Â· y < x Â· z
  Â·-lPosPres< x>0 y<z = subst (_>0) (helper9  _ _ _) (>0-Â· _ _ x>0 y<z)

  Â·-rPosPres< : x > 0r â†’ y < z â†’ y Â· x < z Â· x
  Â·-rPosPres< x>0 y<z = subst (_>0) (helper10 _ _ _) (>0-Â· _ _ y<z x>0)

  Â·-PosPres> : x > 0r â†’ z > 0r â†’ x < y â†’ z < w â†’ x Â· z < y Â· w
  Â·-PosPres> x>0 z>0 x<y z<w = <-trans (Â·-rPosPres< z>0 x<y) (Â·-lPosPres< (<-trans x>0 x<y) z<w)


  +-Pres>0 : x > 0r â†’ y > 0r â†’ x + y > 0r
  +-Pres>0 {x = x} {y = y} = transport (Î» i â†’ >0â‰¡>0r x i â†’ >0â‰¡>0r y i â†’ >0â‰¡>0r (x + y) i) (>0-+ x y)

  Â·-Pres>0 : x > 0r â†’ y > 0r â†’ x Â· y > 0r
  Â·-Pres>0 {x = x} {y = y} = transport (Î» i â†’ >0â‰¡>0r x i â†’ >0â‰¡>0r y i â†’ >0â‰¡>0r (x Â· y) i) (>0-Â· x y)


  -Reverse>0 : x > 0r â†’ - x < 0r
  -Reverse>0 {x = x} x>0 = subst (- x <_) 0Selfinverse (-Reverse< x>0)

  -Reverse<0 : x < 0r â†’ - x > 0r
  -Reverse<0 {x = x} x<0 = subst (- x >_) 0Selfinverse (-Reverse< x<0)

  -Reverse->0 : - x > 0r â†’ x < 0r
  -Reverse->0 {x = x} -x>0 = subst (_< 0r) (-Idempotent x) (-Reverse>0 -x>0)

  -Reverse-<0 : - x < 0r â†’ x > 0r
  -Reverse-<0 {x = x} -x<0 = subst (_> 0r) (-Idempotent x) (-Reverse<0 -x<0)


  >â†’Diff>0 : x > y â†’ x - y > 0r
  >â†’Diff>0 x>y = transport (>0â‰¡>0r _) x>y

  <â†’Diff<0 : x < y â†’ x - y < 0r
  <â†’Diff<0 x<y = subst (_< 0r) (sym (helper2 _ _)) (-Reverse>0 (transport (>0â‰¡>0r _) x<y))

  Diff>0â†’> : x - y > 0r â†’ x > y
  Diff>0â†’> x-y>0 = transport (sym (>0â‰¡>0r _)) x-y>0

  Diff<0â†’< : x - y < 0r â†’ x < y
  Diff<0â†’< x-y<0 = transport (sym (>0â‰¡>0r _)) (subst (_> 0r) (sym (helper2 _ _)) (-Reverse<0 x-y<0))


  Â·-lNegReverse< : x < 0r â†’ y < z â†’ x Â· y > x Â· z
  Â·-lNegReverse< {x = x} {y = y} {z = z} x<0 y<z = transport (Î» i â†’ helper11 x y i > helper11 x z i) -xÂ·y<-xÂ·z
    where -xÂ·y<-xÂ·z : - ((- x) Â· y) > - ((- x) Â· z)
          -xÂ·y<-xÂ·z = -Reverse< (Â·-lPosPres< (-Reverse<0 x<0) y<z)

  Â·-rNegReverse< : x < 0r â†’ y < z â†’ y Â· x > z Â· x
  Â·-rNegReverse< {x = x} {y = y} {z = z} x<0 y<z = transport (Î» i â†’ Â·Comm x y i > Â·Comm x z i) (Â·-lNegReverse< x<0 y<z)


  Â·-rNegReverse>0 : x > 0r â†’ y < 0r â†’ x Â· y < 0r
  Â·-rNegReverse>0 {x = x} {y = y} x>0 y<0 = -Reverse->0 (subst (_> 0r) (helper7 x y) (Â·-Pres>0 x>0 (-Reverse<0 y<0)))

  Â·-lNegReverse>0 : x < 0r â†’ y > 0r â†’ x Â· y < 0r
  Â·-lNegReverse>0 {x = x} {y = y} x<0 y>0 = subst (_< 0r) (Â·Comm y x) (Â·-rNegReverse>0 y>0 x<0)

  Â·-rNegReverse<0 : x < 0r â†’ y < 0r â†’ x Â· y > 0r
  Â·-rNegReverse<0 {x = x} {y = y} x>0 y<0 = subst (_> 0r) (helper8 x y) (Â·-Pres>0 (-Reverse<0 x>0) (-Reverse<0 y<0))


  Â·-rPosCancel>0 : x > 0r â†’ x Â· y > 0r â†’ y > 0r
  Â·-rPosCancel>0 {x = x} {y = y} x>0 xÂ·y>0 with trichotomy y 0r
  ... | lt y<0 = Empty.rec (<-asym (Â·-rNegReverse>0 x>0 y<0) xÂ·y>0)
  ... | eq yâ‰¡0 = Empty.rec (<-arefl xÂ·y>0 (sym (0RightAnnihilates x) âˆ™ (Î» i â†’ x Â· yâ‰¡0 (~ i))))
  ... | gt y>0 = y>0

  Â·-rPosCancel<0 : x > 0r â†’ x Â· y < 0r â†’ y < 0r
  Â·-rPosCancel<0 {x = x} {y = y} x>0 xÂ·y<0 = -Reverse->0 (Â·-rPosCancel>0 x>0 (subst (_> 0r) (sym (helper7 x y)) (-Reverse<0 xÂ·y<0)))

  Â·-lPosCancel>0 : x > 0r â†’ y Â· x > 0r â†’ y > 0r
  Â·-lPosCancel>0 {x = x} {y = y} x>0 yÂ·x>0 = Â·-rPosCancel>0 x>0 (subst (_> 0r) (Â·Comm y x) yÂ·x>0)

  Â·-lPosCancel<0 : x > 0r â†’ y Â· x < 0r â†’ y < 0r
  Â·-lPosCancel<0 {x = x} {y = y} x>0 yÂ·x<0 = Â·-rPosCancel<0 x>0 (subst (_< 0r) (Â·Comm y x) yÂ·x<0)


  Â·-PosÂ·>1â†’> : x > 0r â†’ y > 1r â†’ x Â· y > x
  Â·-PosÂ·>1â†’> x>0 y>1 = subst (_>0) (helper15 _ _) (>0-Â· _ _ x>0 y>1)


  {-

    Non-strict Ordering

  -}

  _â‰¤_ : R â†’ R â†’ Type (â„“-max â„“ â„“')
  x â‰¤ y = (x < y) âŠŽ (x â‰¡ y)

  _â‰¥_ : R â†’ R â†’ Type (â„“-max â„“ â„“')
  x â‰¥ y = y â‰¤ x

  infix 4 _â‰¥_ _â‰¤_

  isPropâ‰¤ : isProp (x â‰¤ y)
  isPropâ‰¤ {x = x} {y = y} (inl x<y) (inl x<y') i = inl (isProp< {x} {y} x<y x<y' i)
  isPropâ‰¤ (inr xâ‰¡y) (inr xâ‰¡y') i = inr (isSetR _ _ xâ‰¡y xâ‰¡y' i)
  isPropâ‰¤ (inl x<y) (inr xâ‰¡y) = Empty.rec (<-arefl x<y xâ‰¡y)
  isPropâ‰¤ (inr xâ‰¡y) (inl x<y) = Empty.rec (<-arefl x<y xâ‰¡y)


  â‰¤-asym : x â‰¤ y â†’ x â‰¥ y â†’ x â‰¡ y
  â‰¤-asym (inr xâ‰¡y) _ = xâ‰¡y
  â‰¤-asym _ (inr yâ‰¡x) = sym yâ‰¡x
  â‰¤-asym {x = x} {y = y} (inl x<y) (inl x>y) = Empty.rec (<-asym {x = x} {y = y} x<y x>y)

  â‰¤-refl : x â‰¡ y â†’ x â‰¤ y
  â‰¤-refl xâ‰¡y = inr xâ‰¡y

  â‰¤-trans : x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
  â‰¤-trans {z = z} (inr xâ‰¡y) yâ‰¤z = subst (_â‰¤ z) (sym xâ‰¡y) yâ‰¤z
  â‰¤-trans {x = x} xâ‰¤y (inr yâ‰¡z) = subst (x â‰¤_) yâ‰¡z xâ‰¤y
  â‰¤-trans {x = x} {y = y} {z = z} (inl x<y) (inl y<z) = inl (<-trans {x = x} {y = y} {z = z} x<y y<z)

  â‰¤-total : (x â‰¤ y) âŠŽ (y â‰¤ x)
  â‰¤-total {x = x} {y = y} with trichotomy x y
  ... | lt x<y = inl (inl x<y)
  ... | eq xâ‰¡y = inl (inr xâ‰¡y)
  ... | gt x>y = inr (inl x>y)


  +-Presâ‰¥0 : x â‰¥ 0r â†’ y â‰¥ 0r â†’ (x + y) â‰¥ 0r
  +-Presâ‰¥0 {x = x} {y = y} (inl x>0) (inl y>0) = inl (+-Pres>0 {x = x} {y = y} x>0 y>0)
  +-Presâ‰¥0 {x = x} {y = y} (inr 0â‰¡x) yâ‰¥0 = subst (_â‰¥ 0r) yâ‰¡x+y yâ‰¥0
    where yâ‰¡x+y : y â‰¡ x + y
          yâ‰¡x+y = sym (+Lid _) âˆ™ (Î» i â†’ 0â‰¡x i + y)
  +-Presâ‰¥0 {x = x} {y = y} xâ‰¥0 (inr 0â‰¡y) = subst (_â‰¥ 0r) xâ‰¡x+y xâ‰¥0
    where xâ‰¡x+y : x â‰¡ x + y
          xâ‰¡x+y = sym (+Rid _) âˆ™ (Î» i â†’ x + 0â‰¡y i)

  Â·-Presâ‰¥0 : x â‰¥ 0r â†’ y â‰¥ 0r â†’ (x Â· y) â‰¥ 0r
  Â·-Presâ‰¥0 {x = x} {y = y} (inl x>0) (inl y>0) = inl (Â·-Pres>0 {x = x} {y = y} x>0 y>0)
  Â·-Presâ‰¥0 {x = x} {y = y} (inr 0â‰¡x) yâ‰¥0 = inr 0â‰¡xÂ·y
    where 0â‰¡xÂ·y : 0r â‰¡ x Â· y
          0â‰¡xÂ·y = sym (0LeftAnnihilates  y) âˆ™ (Î» i â†’ 0â‰¡x i Â· y)
  Â·-Presâ‰¥0 {x = x} {y = y} xâ‰¥0 (inr 0â‰¡y) = inr 0â‰¡xÂ·y
    where 0â‰¡xÂ·y : 0r â‰¡ x Â· y
          0â‰¡xÂ·y = sym (0RightAnnihilates x) âˆ™ (Î» i â†’ x Â· 0â‰¡y i)


  +-rPosâ†’â‰¥ : x â‰¥ 0r â†’ y + x â‰¥ y
  +-rPosâ†’â‰¥ (inl x>0) = inl (+-rPosâ†’> x>0)
  +-rPosâ†’â‰¥ {y = y} (inr 0â‰¡x) = inr (sym (+Rid y) âˆ™ (Î» i â†’ y + 0â‰¡x i))

  +-rNegâ†’â‰¤ : x â‰¤ 0r â†’ y + x â‰¤ y
  +-rNegâ†’â‰¤ (inl x<0) = inl (+-rNegâ†’< x<0)
  +-rNegâ†’â‰¤ {y = y} (inr xâ‰¡0) = inr ((Î» i â†’ y + xâ‰¡0 i) âˆ™ +Rid y)


  â‰¥â†’Diffâ‰¥0 : x â‰¥ y â†’ x - y â‰¥ 0r
  â‰¥â†’Diffâ‰¥0 (inl x>y) = inl (>â†’Diff>0 x>y)
  â‰¥â†’Diffâ‰¥0 {y = y} (inr xâ‰¡y) = inr (sym (+Rinv y) âˆ™ (Î» i â†’ xâ‰¡y i - y))

  Diffâ‰¥0â†’â‰¥ : x - y â‰¥ 0r â†’ x â‰¥ y
  Diffâ‰¥0â†’â‰¥ (inl x-y>0) = inl (Diff>0â†’> x-y>0)
  Diffâ‰¥0â†’â‰¥ {x = x} {y = y} (inr x-yâ‰¡0) = inr (sym (+Lid y) âˆ™ (Î» i â†’ x-yâ‰¡0 i + y) âˆ™ helper19 x y)


  {-

    Strict & Non-strict Together

  -}

  <â‰¤-asym : x < y â†’ y â‰¤ x â†’ âŠ¥
  <â‰¤-asym x<y (inl x>y) = <-asym  x<y x>y
  <â‰¤-asym x<y (inr xâ‰¡y) = <-arefl x<y (sym xâ‰¡y)


  <â‰¤-trans : x < y â†’ y â‰¤ z â†’ x < z
  <â‰¤-trans x<y (inl y<z) = <-trans x<y y<z
  <â‰¤-trans {x = x} x<y (inr yâ‰¡z) = subst (x <_) yâ‰¡z x<y

  â‰¤<-trans : x â‰¤ y â†’ y < z â†’ x < z
  â‰¤<-trans (inl x<y) y<z = <-trans x<y y<z
  â‰¤<-trans {z = z} (inr xâ‰¡y) y<z = subst (_< z) (sym xâ‰¡y) y<z


  Â·-PosPres>â‰¥ : x > 0r â†’ z > 0r â†’ x < y â†’ z â‰¤ w â†’ x Â· z < y Â· w
  Â·-PosPres>â‰¥ x>0 z>0 x<y (inl z<w) = Â·-PosPres> x>0 z>0 x<y z<w
  Â·-PosPres>â‰¥ {x = x} {z = z} {y = y} x>0 z>0 x<y (inr zâ‰¡w) =
    subst (Î» w â†’ x Â· z < y Â· w) zâ‰¡w (Â·-rPosPres< z>0 x<y)


  {-

    Ordered Ring is Integral

  -}

  Â·-lCancel : Â¬ x â‰¡ 0r â†’ x Â· y â‰¡ x Â· z â†’ y â‰¡ z
  Â·-lCancel {x = x} {y = y} {z = z} xâ‰¢0 xÂ·yâ‰¡xÂ·z with trichotomy x 0r | trichotomy y z
  ... | _      | eq yâ‰¡z = yâ‰¡z
  ... | eq xâ‰¡0 | _      = Empty.rec (xâ‰¢0 xâ‰¡0)
  ... | lt x<0 | lt y<z = Empty.rec (<-arefl (Â·-lNegReverse< x<0 y<z) (sym xÂ·yâ‰¡xÂ·z))
  ... | lt x<0 | gt y>z = Empty.rec (<-arefl (Â·-lNegReverse< x<0 y>z) xÂ·yâ‰¡xÂ·z)
  ... | gt x>0 | lt y<z = Empty.rec (<-arefl (Â·-lPosPres< x>0 y<z) xÂ·yâ‰¡xÂ·z)
  ... | gt x>0 | gt y>z = Empty.rec (<-arefl (Â·-lPosPres< x>0 y>z) (sym xÂ·yâ‰¡xÂ·z))

  Â·-rCancel : Â¬ x â‰¡ 0r â†’ y Â· x â‰¡ z Â· x â†’ y â‰¡ z
  Â·-rCancel xâ‰¢0 yÂ·xâ‰¡zÂ·x = Â·-lCancel xâ‰¢0 (Â·Comm _ _ âˆ™ yÂ·xâ‰¡zÂ·x âˆ™ Â·Comm _ _)


  {-

    Inclusion from Natural Numbers

  -}

  1>0 : 1r > 0r
  1>0 = subst (_>0) (sym helper16) (>0-1r)


  â„•â†’R-Pos : â„• â†’ R
  â„•â†’R-Pos 0 = 0r
  â„•â†’R-Pos 1 = 1r
  â„•â†’R-Pos (suc (suc n)) = 1r + â„•â†’R-Pos (suc n)

  â„•â†’R-Neg : â„• â†’ R
  â„•â†’R-Neg n = - â„•â†’R-Pos n

  â„•â†’R-PosSuc : (n : â„•) â†’ â„•â†’R-Pos (suc n) â‰¡ 1r + â„•â†’R-Pos n
  â„•â†’R-PosSuc zero = sym (+Rid 1r)
  â„•â†’R-PosSuc (suc n) = refl

  â„•â†’R-NegSuc : (n : â„•) â†’ â„•â†’R-Neg (suc n) â‰¡ - 1r + â„•â†’R-Neg n
  â„•â†’R-NegSuc n = (Î» i â†’ - â„•â†’R-PosSuc n i) âˆ™ helper18 _


  â„•â†’R-PosSuc>0 : (n : â„•) â†’ â„•â†’R-Pos (suc n) > 0r
  â„•â†’R-PosSuc>0 zero = 1>0
  â„•â†’R-PosSuc>0 (suc n) = +-Pres>0 1>0 (â„•â†’R-PosSuc>0 n)

  â„•â†’R-Posâ‰¥0 : (n : â„•) â†’ â„•â†’R-Pos n â‰¥ 0r
  â„•â†’R-Posâ‰¥0 zero = inr refl
  â„•â†’R-Posâ‰¥0 (suc n) = inl (â„•â†’R-PosSuc>0 n)

  â„•â†’R-NegSuc<0 : (n : â„•) â†’ â„•â†’R-Neg (suc n) < 0r
  â„•â†’R-NegSuc<0 n = -Reverse>0 (â„•â†’R-PosSuc>0 n)

  â„•â†’R-Negâ‰¤0 : (n : â„•) â†’ â„•â†’R-Neg n â‰¤ 0r
  â„•â†’R-Negâ‰¤0 zero = inr 0Selfinverse
  â„•â†’R-Negâ‰¤0 (suc n) = inl (â„•â†’R-NegSuc<0 n)


  -1r : R
  -1r = - 1r

  2r : R
  2r = 1r + 1r

  -1<0 : -1r < 0r
  -1<0 = â„•â†’R-NegSuc<0 0

  2>0 : 2r > 0r
  2>0 = â„•â†’R-PosSuc>0 1


  q+1>q : {q : R} â†’ q + 1r > q
  q+1>q {q = q} = +-rPosâ†’> {x = 1r} {y = q} 1>0

  q-1<q : {q : R} â†’ q - 1r < q
  q-1<q {q = q} = +-rNegâ†’< {x = -1r} {y = q} -1<0



  {-

    Scalar multiplication by natural numbers

  -}

  _â‹†_ : â„• â†’ R â†’ R
  n â‹† q = â„•â†’R-Pos n Â· q


  0â‹†qâ‰¡0 : (q : R) â†’ 0 â‹† q â‰¡ 0r
  0â‹†qâ‰¡0 q = 0LeftAnnihilates q

  1â‹†qâ‰¡q : (q : R) â†’ 1 â‹† q â‰¡ q
  1â‹†qâ‰¡q q = Â·Lid q

  sucnâ‹†qâ‰¡nâ‹†q+q : (n : â„•)(q : R) â†’ (suc n) â‹† q â‰¡ (n â‹† q) + q
  sucnâ‹†qâ‰¡nâ‹†q+q n q = (Î» i â†’ â„•â†’R-PosSuc n i Â· q) âˆ™ helper17 (â„•â†’R-Pos n) q

  sucnâ‹†q>0 : (n : â„•)(q : R) â†’ q > 0r â†’ (suc n) â‹† q > 0r
  sucnâ‹†q>0 zero q q>0 = subst (_> 0r) (sym (1â‹†qâ‰¡q q)) q>0
  sucnâ‹†q>0 (suc n) q q>0 = subst (_> 0r) (sym (sucnâ‹†qâ‰¡nâ‹†q+q (suc n) q))
    (+-Pres>0 {x = suc n â‹† q} (sucnâ‹†q>0 n q q>0) q>0)

  nâ‹†qâ‰¥0 : (n : â„•)(q : R) â†’ q > 0r â†’ n â‹† q â‰¥ 0r
  nâ‹†qâ‰¥0 zero q _ = inr (sym (0â‹†qâ‰¡0 q))
  nâ‹†qâ‰¥0 (suc n) q q>0 = inl (sucnâ‹†q>0 n q q>0)
