{-

The Dedekind Cut

-}
{-# OPTIONS --safe #-}
module Classical.Algebra.OrderedField.DedekindCut.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels

open import Cubical.Data.Sigma
open import Cubical.Data.Empty as Empty
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.Relation.Nullary
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRingSolver.Reflection

open import Classical.Axioms
open import Classical.Foundations.Powerset
open import Classical.Algebra.OrderedField

private
  variable
    â„“ â„“' : Level


private
  module Helpers {â„“ : Level}(ğ“¡ : CommRing â„“) where
    open CommRingStr (ğ“¡ .snd)

    helper1 : (a b c d : ğ“¡ .fst) â†’ c + ((a + b) - d) â‰¡ a + (b + (c - d))
    helper1 = solve ğ“¡

    helper2 : (c d : ğ“¡ .fst) â†’ c â‰¡ c + (d - d)
    helper2 = solve ğ“¡

    helper1' : (a b c d : ğ“¡ .fst) â†’ c Â· ((a Â· b) Â· d) â‰¡ a Â· (b Â· (c Â· d))
    helper1' = solve ğ“¡


module Basics â¦ƒ ğŸ¤– : Oracle â¦„
  (ğ’¦ : OrderedField â„“ â„“')
  where

  open Oracle ğŸ¤–

  private
    K = ğ’¦ .fst .fst .fst


  open OrderedFieldStr ğ’¦

  open Helpers (ğ’¦ .fst .fst)


  {-

    Dedekind Cut

  -}

  record DedekindCut : Type (â„“-max â„“ â„“') where
    no-eta-equality

    field

      upper : â„™ K
      upper-inhab : âˆ¥ Î£[ q âˆˆ K ] q âˆˆ upper âˆ¥
      upper-close : (r : K)(q : K) â†’ q âˆˆ upper â†’ q < r â†’ r âˆˆ upper
      upper-round : (q : K) â†’ q âˆˆ upper â†’ âˆ¥ Î£[ r âˆˆ K ] (r < q) Ã— (r âˆˆ upper) âˆ¥
      lower-inhab : âˆ¥ Î£[ q âˆˆ K ] ((r : K) â†’ r âˆˆ upper â†’ q < r) âˆ¥


  open DedekindCut


  -- Dedekind Completion of K

  ğ•‚ : Type (â„“-max â„“ â„“')
  ğ•‚ = DedekindCut


  {-

    hLevel of ğ•‚

  -}

  -- Construct path in ğ•‚ from path of two cuts

  path-ğ•‚ : (a b : DedekindCut) â†’ a .upper â‰¡ b .upper â†’ a â‰¡ b
  path-ğ•‚ a b upper-path i .upper = upper-path i
  path-ğ•‚ a b upper-path i .upper-inhab =
    isPropâ†’PathP (Î» i â†’ squash {A = Î£[ q âˆˆ K ] q âˆˆ upper-path i}) (a .upper-inhab) (b .upper-inhab) i
  path-ğ•‚ a b upper-path i .upper-close =
    isPropâ†’PathP (Î» i â†’ isPropÎ 4 {C = Î» _ q â†’ q âˆˆ upper-path i} (Î» _ _ _ _ â†’ isPropâˆˆ (upper-path i)))
    (a .upper-close) (b .upper-close) i
  path-ğ•‚ a b upper-path i .upper-round =
    isPropâ†’PathP (Î» i â†’ isPropÎ 2 {B = Î» q â†’ q âˆˆ upper-path i}
      (Î» q _ â†’ squash {A = Î£[ r âˆˆ K ] (r < q) Ã— (r âˆˆ upper-path i)}))
    (a .upper-round) (b .upper-round) i
  path-ğ•‚ a b upper-path i .lower-inhab =
    isPropâ†’PathP (Î» i â†’ squash {A = Î£[ q âˆˆ K ] ((r : K) â†’ r âˆˆ upper-path i â†’ q < r)})
    (a .lower-inhab) (b .lower-inhab) i

  -- ğ•‚ is hSet

  isSetğ•‚ : isSet ğ•‚
  isSetğ•‚ a b p q i j =
    hcomp (Î» k â†’ Î»
      { (i = i0) â†’ path-ğ•‚ (lift-square i0 j) (p j) refl k
      ; (i = i1) â†’ path-ğ•‚ (lift-square i1 j) (q j) refl k
      ; (j = i0) â†’ path-ğ•‚ a a refl k
      ; (j = i1) â†’ path-ğ•‚ b b refl k })
    (lift-square i j)
    where
    lift-square : (i j : I) â†’ ğ•‚
    lift-square i j = path-ğ•‚ a b
      (isSetâ†’SquareP (Î» _ _ â†’ isSetâ„™ {X = K}) (cong upper p) (cong upper q) refl refl i) j


  {-

    Inclusion of K into ğ•‚

  -}

  -- Rational number is real number

  _<P_ : K â†’ K â†’ hProp â„“'
  _<P_ p q = (p < q) , isProp<

  Kâ†’ğ•‚ : K â†’ ğ•‚
  Kâ†’ğ•‚ q .upper = specify (q <P_)
  Kâ†’ğ•‚ q .upper-inhab = âˆ£ q + 1r , Inhabâ†’âˆˆ (q <P_) q+1>q âˆ£
  Kâ†’ğ•‚ q .upper-close r s sâˆˆupper r>s = Inhabâ†’âˆˆ (q <P_) (<-trans (âˆˆâ†’Inhab (q <P_) sâˆˆupper) r>s)
  Kâ†’ğ•‚ q .upper-round r râˆˆupper = âˆ£ middle q r , middle<r {p = q} {q = r} r>q , Inhabâ†’âˆˆ (q <P_) (middle>l r>q) âˆ£
    where r>q : r > q
          r>q = âˆˆâ†’Inhab (q <P_) râˆˆupper
  Kâ†’ğ•‚ q .lower-inhab = âˆ£ q - 1r , (Î» r râˆˆupper â†’ <-trans q-1<q (âˆˆâ†’Inhab (q <P_) râˆˆupper)) âˆ£


  -- Zero and Unit

  ğŸ˜ : ğ•‚
  ğŸ˜ = Kâ†’ğ•‚ 0r

  ğŸ™ : ğ•‚
  ğŸ™ = Kâ†’ğ•‚ 1r


  {-

    The natural order on ğ•‚

  -}

  _â‰¤ğ•‚_ : ğ•‚ â†’ ğ•‚ â†’ Type â„“
  a â‰¤ğ•‚ b = b .upper âŠ† a .upper

  _â‰¥ğ•‚_ : ğ•‚ â†’ ğ•‚ â†’ Type â„“
  a â‰¥ğ•‚ b = b â‰¤ğ•‚ a

  isPropâ‰¤ğ•‚ : {a b : ğ•‚} â†’ isProp (a â‰¤ğ•‚ b)
  isPropâ‰¤ğ•‚ = isPropâŠ†

  â‰¤ğ•‚-refl : {a b : ğ•‚} â†’ a â‰¡ b â†’ a â‰¤ğ•‚ b
  â‰¤ğ•‚-refl aâ‰¡b {x = q} qâˆˆupper = subst (Î» p â†’ q âˆˆ p .upper) (sym aâ‰¡b) qâˆˆupper

  â‰¤ğ•‚-asym : {a b : ğ•‚} â†’ a â‰¤ğ•‚ b â†’ b â‰¤ğ•‚ a â†’ a â‰¡ b
  â‰¤ğ•‚-asym aâ‰¤b bâ‰¤a = path-ğ•‚ _ _ (biâŠ†â†’â‰¡ bâ‰¤a aâ‰¤b)


  {-

    Non-membership of upper cut

  -}

  module _ (a : ğ•‚)(q : K) where

    Â¬âˆˆupperâ†’<upper : Â¬ q âˆˆ a .upper â†’ (r : K) â†’ r âˆˆ a .upper â†’ q < r
    Â¬âˆˆupperâ†’<upper Â¬qâˆˆupper r râˆˆupper = case-split (trichotomy q r)
      where
      case-split : Trichotomy q r â†’ q < r
      case-split (lt q<r) = q<r
      case-split (eq qâ‰¡r) = Empty.rec (Â¬qâˆˆupper (subst (_âˆˆ a .upper) (sym qâ‰¡r) râˆˆupper))
      case-split (gt q>r) = Empty.rec (Â¬qâˆˆupper (a .upper-close _ _ râˆˆupper q>r))

    <upperâ†’Â¬âˆˆupper : ((r : K) â†’ r âˆˆ a .upper â†’ q < r) â†’ Â¬ q âˆˆ a .upper
    <upperâ†’Â¬âˆˆupper q<râˆˆupper = case-split (decide (isPropâˆˆ (a .upper)))
      where
      case-split : Dec (q âˆˆ a .upper) â†’ Â¬ q âˆˆ a .upper
      case-split (yes p) _ = <-arefl (q<râˆˆupper q p) refl
      case-split (no Â¬p) = Â¬p


  Â¬âˆˆupper-close : (a : ğ•‚)(p q : K) â†’ Â¬ q âˆˆ a .upper â†’ p < q â†’ Â¬ p âˆˆ a .upper
  Â¬âˆˆupper-close a p q Â¬qâˆˆupper p<q =
    <upperâ†’Â¬âˆˆupper a _ (Î» r râˆˆupper â†’ <-trans p<q (Â¬âˆˆupperâ†’<upper a _ Â¬qâˆˆupper r râˆˆupper))


  {-

    Basic algebraic operations of ğ•‚

  -}


  -- Additive Inverse

  -upper : (a : ğ•‚)(x : K) â†’ hProp (â„“-max â„“ â„“')
  -upper a x = âˆ¥ Î£[ p âˆˆ K ] ((r : K) â†’ r âˆˆ a .upper â†’ p < r) Ã— (x > - p) âˆ¥ , squash

  -ğ•‚_ : ğ•‚ â†’ ğ•‚
  (-ğ•‚ a) .upper = specify (-upper a)
  (-ğ•‚ a) .upper-inhab = Prop.map
    (Î» (q , q<râˆˆupper) â†’
      (- q) + 1r , Inhabâ†’âˆˆ (-upper a) âˆ£ q , q<râˆˆupper , q+1>q âˆ£ )
    (a .lower-inhab)
  (-ğ•‚ a) .upper-close r q qâˆˆupper q<r = Prop.rec (isPropâˆˆ ((-ğ•‚ a) .upper))
    (Î» (p , p<râˆˆupper , q>-p) â†’
      Inhabâ†’âˆˆ (-upper a) âˆ£ p , p<râˆˆupper , <-trans q>-p q<r âˆ£)
    (âˆˆâ†’Inhab (-upper a) qâˆˆupper)
  (-ğ•‚ a) .upper-round q qâˆˆupper = Prop.map
    (Î» (p , p<râˆˆupper , q>-p) â†’
      middle (- p) q , middle<r q>-p  , Inhabâ†’âˆˆ (-upper a) âˆ£ p , p<râˆˆupper , middle>l q>-p âˆ£)
    (âˆˆâ†’Inhab (-upper a) qâˆˆupper)
  (-ğ•‚ a) .lower-inhab = Prop.map
    (Î» (q , qâˆˆupper) â†’
      - q , Î» r râˆˆupper â†’ Prop.rec isProp<
        (Î» (p , p<sâˆˆupper , r>-p) â†’
          <-trans (-Reverse< (p<sâˆˆupper q qâˆˆupper)) r>-p)
        (âˆˆâ†’Inhab (-upper a) râˆˆupper))
    (a .upper-inhab)


  -- Addition

  +upper : ğ•‚ â†’ ğ•‚ â†’ K â†’ hProp â„“
  +upper a b r = âˆ¥ Î£[ p âˆˆ K ] Î£[ q âˆˆ K ] p âˆˆ a .upper Ã— q âˆˆ b .upper Ã— (r â‰¡ p + q) âˆ¥ , squash

  private
    alg-helper : (a b c d : K) â†’ d â‰¡ a + b â†’ c â‰¡ a + (b + (c - d))
    alg-helper a b c d dâ‰¡a+b = helper2 c d âˆ™ (Î» i â†’ c + (dâ‰¡a+b i - d)) âˆ™ helper1 a b c d

  _+ğ•‚_ : ğ•‚ â†’ ğ•‚ â†’ ğ•‚
  (a +ğ•‚ b) .upper = specify (+upper a b)
  (a +ğ•‚ b) .upper-inhab = Prop.map2
    (Î» (p , pâˆˆupper) (q , qâˆˆupper) â†’
      p + q , Inhabâ†’âˆˆ (+upper a b) âˆ£ p , q , pâˆˆupper , qâˆˆupper , refl âˆ£)
    (a .upper-inhab) (b .upper-inhab)
  (a +ğ•‚ b) .upper-close r q qâˆˆupper q<r = Prop.rec (isPropâˆˆ ((a +ğ•‚ b) .upper))
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡s+t) â†’
      let t+r-qâˆˆupper : (t + (r - q)) âˆˆ b .upper
          t+r-qâˆˆupper = b .upper-close _ _ tâˆˆupper (+-rPosâ†’> (>â†’Diff>0 q<r))
          râ‰¡s+t+r-q : r â‰¡ s + (t + (r - q))
          râ‰¡s+t+r-q = alg-helper s t r q qâ‰¡s+t
      in  Inhabâ†’âˆˆ (+upper a b) âˆ£ s , t + (r - q) , sâˆˆupper , t+r-qâˆˆupper , râ‰¡s+t+r-q âˆ£)
    (âˆˆâ†’Inhab (+upper a b) qâˆˆupper)
  (a +ğ•‚ b) .upper-round q qâˆˆupper = Prop.rec squash
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡s+t) â†’ Prop.map2
      (Î» (s' , s'<s , s'âˆˆupper) (t' , t'<t , t'âˆˆupper) â†’
        s' + t' , subst (s' + t' <_) (sym qâ‰¡s+t) (+-Pres< s'<s t'<t) ,
        Inhabâ†’âˆˆ (+upper a b) âˆ£ s' , t' , s'âˆˆupper , t'âˆˆupper , refl âˆ£)
      (a .upper-round s sâˆˆupper) (b .upper-round t tâˆˆupper))
    (âˆˆâ†’Inhab (+upper a b) qâˆˆupper)
  (a +ğ•‚ b) .lower-inhab = Prop.map2
    (Î» (p , p<râˆˆupper) (q , q<râˆˆupper) â†’
        p + q , Î» r râˆˆupper â†’ Prop.rec isProp<
          (Î» (s , t , sâˆˆupper , tâˆˆupper , râ‰¡s+t) â†’
            subst (p + q <_) (sym râ‰¡s+t)
            (+-Pres< (p<râˆˆupper s sâˆˆupper) (q<râˆˆupper t tâˆˆupper)))
          (âˆˆâ†’Inhab (+upper a b) râˆˆupper))
    (a .lower-inhab) (b .lower-inhab)


  {-

    Non-Negative Reals

  -}

  ğ•‚â‚Š : Type (â„“-max â„“ â„“')
  ğ•‚â‚Š = Î£[ r âˆˆ ğ•‚ ] (r â‰¥ğ•‚ ğŸ˜)

  path-ğ•‚â‚Š : (a b : ğ•‚â‚Š) â†’ a .fst â‰¡ b .fst â†’ a â‰¡ b
  path-ğ•‚â‚Š a b p i .fst = p i
  path-ğ•‚â‚Š a b p i .snd = isPropâ†’PathP (Î» i â†’ isPropâ‰¤ğ•‚ {a = ğŸ˜} {b = p i}) (a .snd) (b .snd) i

  qâˆˆğ•‚â‚Šâ†’q>0 : (a : ğ•‚â‚Š)(q : K) â†’ q âˆˆ a .fst .upper â†’ q > 0r
  qâˆˆğ•‚â‚Šâ†’q>0 a q qâˆˆupper = âˆˆâ†’Inhab (0r <P_) (a .snd qâˆˆupper)


  -- Zero and Unit

  ğŸ˜â‚Š : ğ•‚â‚Š
  ğŸ˜â‚Š = ğŸ˜ , â‰¡â†’âŠ† {A = ğŸ˜ .upper} refl

  ğŸ™â‚Š : ğ•‚â‚Š
  ğŸ™â‚Š = ğŸ™ , Î» qâˆˆupper â†’ Inhabâ†’âˆˆ (0r <P_) (<-trans 1>0 (âˆˆâ†’Inhab (1r <P_) qâˆˆupper))


  -- Addition

  +upperâ‚Š : ğ•‚â‚Š â†’ ğ•‚â‚Š â†’ K â†’ hProp â„“
  +upperâ‚Š a b = +upper (a .fst) (b .fst)

  _+ğ•‚â‚Š_ : (a b : ğ•‚â‚Š) â†’ ğ•‚â‚Š
  ((a , aâ‰¥0) +ğ•‚â‚Š (b , bâ‰¥0)) .fst = a +ğ•‚ b
  ((a , aâ‰¥0) +ğ•‚â‚Š (b , bâ‰¥0)) .snd qâˆˆupper =
    Prop.rec (isPropâˆˆ (ğŸ˜ .upper))
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡s+t) â†’
      let s>0 = âˆˆâ†’Inhab (0r <P_) (aâ‰¥0 sâˆˆupper)
          t>0 = âˆˆâ†’Inhab (0r <P_) (bâ‰¥0 tâˆˆupper)
      in  Inhabâ†’âˆˆ (0r <P_) (subst (_> 0r) (sym qâ‰¡s+t) (+-Pres>0 s>0 t>0)))
    (âˆˆâ†’Inhab (+upper a b) qâˆˆupper)


  -- Multiplication

  Â·upper : ğ•‚ â†’ ğ•‚ â†’ K â†’ hProp â„“
  Â·upper a b r = âˆ¥ Î£[ p âˆˆ K ] Î£[ q âˆˆ K ] p âˆˆ a .upper Ã— q âˆˆ b .upper Ã— (r â‰¡ p Â· q) âˆ¥ , squash

  Â·upperâ‚Š : ğ•‚â‚Š â†’ ğ•‚â‚Š â†’ K â†’ hProp â„“
  Â·upperâ‚Š a b = Â·upper (a .fst) (b .fst)


  â‰¥ğ•‚0+qâˆˆupperâ†’q>0 : (a : ğ•‚){q : K} â†’ a â‰¥ğ•‚ ğŸ˜ â†’ q âˆˆ a .upper â†’ q > 0r
  â‰¥ğ•‚0+qâˆˆupperâ†’q>0 a {q = q} aâ‰¥0 qâˆˆupper = âˆˆâ†’Inhab (0r <P_) (aâ‰¥0 qâˆˆupper)

  qâˆˆÂ·upperâ†’q>0 : (a b : ğ•‚) â†’ a â‰¥ğ•‚ ğŸ˜ â†’ b â‰¥ğ•‚ ğŸ˜ â†’ (q : K) â†’ q âˆˆ specify (Â·upper a b) â†’ q > 0r
  qâˆˆÂ·upperâ†’q>0 a b aâ‰¥0 bâ‰¥0 q qâˆˆupper = Prop.rec isProp<
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡sÂ·t) â†’
      subst (_> 0r) (sym qâ‰¡sÂ·t)
        (Â·-Pres>0 (â‰¥ğ•‚0+qâˆˆupperâ†’q>0 a aâ‰¥0 sâˆˆupper) (â‰¥ğ•‚0+qâˆˆupperâ†’q>0 b bâ‰¥0 tâˆˆupper)))
    (âˆˆâ†’Inhab (Â·upper a b) qâˆˆupper)

  private
    alg-helper' : (a b c d : K)(dâ‰¢0 : Â¬ d â‰¡ 0r) â†’ d â‰¡ a Â· b â†’ c â‰¡ a Â· (b Â· (c Â· inv dâ‰¢0))
    alg-helper' a b c d dâ‰¢0 dâ‰¡aÂ·b =
        sym (Â·Rid c) âˆ™ (Î» i â†’ c Â· Â·-rInv dâ‰¢0 (~ i))
      âˆ™ (Î» i â†’ c Â· (dâ‰¡aÂ·b i Â· inv dâ‰¢0)) âˆ™ helper1' a b c (inv dâ‰¢0)

  _Â·ğ•‚â‚Š_ : (a b : ğ•‚â‚Š) â†’ ğ•‚â‚Š
  ((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .fst .upper = specify (Â·upper a b)
  ((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .fst .upper-inhab = Prop.map2
    (Î» (p , pâˆˆupper) (q , qâˆˆupper) â†’
      p Â· q , Inhabâ†’âˆˆ (Â·upper a b) âˆ£ p , q , pâˆˆupper , qâˆˆupper , refl âˆ£)
    (a .upper-inhab) (b .upper-inhab)
  ((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .fst .upper-close r q qâˆˆupper q<r =
    Prop.rec (isPropâˆˆ (((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .fst .upper))
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡sÂ·t) â†’
      let q>0 : q > 0r
          q>0 = qâˆˆÂ·upperâ†’q>0 a b aâ‰¥0 bâ‰¥0 q qâˆˆupper
          qâ‰¢0 : Â¬ q â‰¡ 0r
          qâ‰¢0 = >-arefl q>0
          qâ»Â¹ = inv qâ‰¢0
          tÂ·rÂ·qâ»Â¹âˆˆupper : (t Â· (r Â· qâ»Â¹)) âˆˆ b .upper
          tÂ·rÂ·qâ»Â¹âˆˆupper = b .upper-close _ _ tâˆˆupper
            (Â·-PosÂ·>1â†’> (â‰¥ğ•‚0+qâˆˆupperâ†’q>0 b bâ‰¥0 tâˆˆupper) (p>q>0â†’pÂ·qâ»Â¹>1 q>0 q<r))
          râ‰¡sÂ·tÂ·rÂ·qâ»Â¹ : r â‰¡ s Â· (t Â· (r Â· qâ»Â¹))
          râ‰¡sÂ·tÂ·rÂ·qâ»Â¹ = alg-helper' s t r q qâ‰¢0 qâ‰¡sÂ·t
      in  Inhabâ†’âˆˆ (Â·upper a b) âˆ£ s , t Â· (r Â· qâ»Â¹) , sâˆˆupper , tÂ·rÂ·qâ»Â¹âˆˆupper , râ‰¡sÂ·tÂ·rÂ·qâ»Â¹ âˆ£)
    (âˆˆâ†’Inhab (Â·upper a b) qâˆˆupper)
  ((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .fst .upper-round q qâˆˆupper = Prop.rec squash
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡sÂ·t) â†’ Prop.map2
      (Î» (s' , s'<s , s'âˆˆupper) (t' , t'<t , t'âˆˆupper) â†’
        s' Â· t' , subst (s' Â· t' <_) (sym qâ‰¡sÂ·t)
          (Â·-PosPres> (â‰¥ğ•‚0+qâˆˆupperâ†’q>0 a aâ‰¥0 s'âˆˆupper) (â‰¥ğ•‚0+qâˆˆupperâ†’q>0 b bâ‰¥0 t'âˆˆupper) s'<s t'<t) ,
        Inhabâ†’âˆˆ (Â·upper a b) âˆ£ s' , t' , s'âˆˆupper , t'âˆˆupper , refl âˆ£ )
      (a .upper-round s sâˆˆupper) (b .upper-round t tâˆˆupper))
    (âˆˆâ†’Inhab (Â·upper a b) qâˆˆupper)
  ((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .fst .lower-inhab =
    âˆ£ - 1r , (Î» r râˆˆupper â†’ <-trans -1<0 (qâˆˆÂ·upperâ†’q>0 a b aâ‰¥0 bâ‰¥0 r râˆˆupper)) âˆ£
  ((a , aâ‰¥0) Â·ğ•‚â‚Š (b , bâ‰¥0)) .snd qâˆˆupper =
    Prop.rec (isPropâˆˆ (ğŸ˜ .upper))
    (Î» (s , t , sâˆˆupper , tâˆˆupper , qâ‰¡sÂ·t) â†’
      let s>0 = âˆˆâ†’Inhab (0r <P_) (aâ‰¥0 sâˆˆupper)
          t>0 = âˆˆâ†’Inhab (0r <P_) (bâ‰¥0 tâˆˆupper)
      in  Inhabâ†’âˆˆ (0r <P_) (subst (_> 0r) (sym qâ‰¡sÂ·t) (Â·-Pres>0 s>0 t>0)))
    (âˆˆâ†’Inhab (Â·upper a b) qâˆˆupper)


  -- Multiplicative Inverse

  inv-upper : (a : ğ•‚)(x : K) â†’ hProp (â„“-max â„“ â„“')
  inv-upper a x = âˆ¥ Î£[ p âˆˆ K ] Î£[ p>0 âˆˆ p > 0r ] ((r : K) â†’ r âˆˆ a .upper â†’ p < r) Ã— (x > inv (>-arefl p>0)) âˆ¥ , squash

  invğ•‚â‚Š : (a : ğ•‚)(q : K) â†’ q > 0r â†’ ((r : K) â†’ r âˆˆ a .upper â†’ q < r) â†’ ğ•‚â‚Š
  invğ•‚â‚Š a _ _ _ .fst .upper = specify (inv-upper a)
  invğ•‚â‚Š a qâ‚€ qâ‚€>0 qâ‚€<râˆˆupper .fst .upper-inhab =
    let qâ‚€â»Â¹ = inv (>-arefl qâ‚€>0) in
    âˆ£ qâ‚€â»Â¹ + 1r , Inhabâ†’âˆˆ (inv-upper a) âˆ£ qâ‚€ , qâ‚€>0 , qâ‚€<râˆˆupper , q+1>q {q = qâ‚€â»Â¹} âˆ£ âˆ£
  invğ•‚â‚Š a qâ‚€ qâ‚€>0 qâ‚€<râˆˆupper .fst .upper-close r q qâˆˆupper q<r =
    Prop.rec (isPropâˆˆ (invğ•‚â‚Š a qâ‚€ qâ‚€>0 qâ‚€<râˆˆupper .fst .upper))
    (Î» (p , p>0 , p<râˆˆupper , q>pâ»Â¹) â†’
      Inhabâ†’âˆˆ (inv-upper a) âˆ£ p , p>0 , p<râˆˆupper , <-trans q>pâ»Â¹ q<r âˆ£)
    (âˆˆâ†’Inhab (inv-upper a) qâˆˆupper)
  invğ•‚â‚Š a _ _ _ .fst .upper-round q qâˆˆupper = Prop.map
    (Î» (p , p>0 , p<râˆˆupper , q>pâ»Â¹) â†’
      let pâ»Â¹ = inv (>-arefl p>0) in
      middle pâ»Â¹ q , middle<r q>pâ»Â¹  , Inhabâ†’âˆˆ (inv-upper a) âˆ£ p , p>0 , p<râˆˆupper , middle>l q>pâ»Â¹ âˆ£)
    (âˆˆâ†’Inhab (inv-upper a) qâˆˆupper)
  invğ•‚â‚Š a qâ‚€ qâ‚€>0 qâ‚€<râˆˆupper .fst .lower-inhab = Prop.map
    (Î» (q , qâˆˆupper) â†’
      let q>0 = <-trans qâ‚€>0 (qâ‚€<râˆˆupper q qâˆˆupper)
          qâ»Â¹ = inv (>-arefl q>0) in
      qâ»Â¹ , Î» r râˆˆupper â†’ Prop.rec isProp<
        (Î» (p , p>0 , p<sâˆˆupper , r>pâ»Â¹) â†’
          <-trans (inv-Reverse< _ _ (p<sâˆˆupper q qâˆˆupper)) r>pâ»Â¹)
        (âˆˆâ†’Inhab (inv-upper a) râˆˆupper))
    (a .upper-inhab)
  invğ•‚â‚Š a _ _ _ .snd qâˆˆupper =
    Prop.rec (isPropâˆˆ (ğŸ˜ .upper))
    (Î» (p , p>0 , p<râˆˆupper , q>pâ»Â¹) â†’
      Inhabâ†’âˆˆ (0r <P_) (<-trans (p>0â†’pâ»Â¹>0 p>0) q>pâ»Â¹))
    (âˆˆâ†’Inhab (inv-upper a) qâˆˆupper)
