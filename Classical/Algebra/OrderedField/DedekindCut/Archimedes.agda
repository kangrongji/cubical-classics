{-

A Technical Lemma about Archimedean-ness

-}
{-# OPTIONS --safe #-}
module Classical.Algebra.OrderedField.DedekindCut.Archimedes where

open import Cubical.Foundations.Prelude
open import Cubical.Data.Sigma
open import Cubical.Data.Nat using (â„• ; zero ; suc)
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.Relation.Nullary
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.RingSolver.Reflection

open import Classical.Preliminary.Nat
open import Classical.Axioms.ExcludedMiddle
open import Classical.Foundations.Powerset

open import Classical.Algebra.OrderedRing.Archimedes
open import Classical.Algebra.OrderedField
open import Classical.Algebra.OrderedField.DedekindCut.Base

private
  variable
    â„“ â„“' : Level

private
  module Helpers {â„“ : Level}(ð“¡ : CommRing â„“) where
    open CommRingStr (ð“¡ .snd)

    helper1 : (p q : ð“¡ .fst) â†’ p + (q - p) â‰¡ q
    helper1 = solve ð“¡

    helper2 : (r u Îµ : ð“¡ .fst) â†’ (r + Îµ) - u â‰¡ (r - u) + Îµ
    helper2 = solve ð“¡

    helper3 : (a b c : ð“¡ .fst) â†’ (a - b) + (b - c) â‰¡ a - c
    helper3 = solve ð“¡

    helper4 : (b c : ð“¡ .fst) â†’ c + (b - c) â‰¡ b
    helper4 = solve ð“¡


module Archimedes (decide : LEM)
  (ð’¦ : OrderedField â„“ â„“')(archimedesK : isArchimedean (ð’¦ . fst))
  where

  private
    K = ð’¦ .fst .fst .fst

  open Powerset decide

  open OrderedFieldStr ð’¦
  open Basics   decide ð’¦
  open DedekindCut

  open Helpers (ð’¦ .fst .fst)


  module _  (a : ð•‚)(Îµ : K)(Îµ>0 : Îµ > 0r) where

    module _
      (p : K)(Â¬pâˆˆupper : Â¬ p âˆˆ a .upper)
      where

      private
        P : â„• â†’ Type
        P n = (p + n â‹† Îµ) âˆˆ a .upper

        isPropP : (n : â„•) â†’ isProp (P n)
        isPropP _ = isPropâˆˆ (a .upper)

        decP : (n : â„•) â†’ Dec (P n)
        decP n = decide (isPropP n)

        Â¬P0 : Â¬ P zero
        Â¬P0 p0 = Â¬pâˆˆupper (subst (_âˆˆ a .upper) ((Î» i â†’ p + 0â‹†qâ‰¡0 Îµ i) âˆ™ +Rid p) p0)

        âˆƒPn : âˆ¥ Î£[ n âˆˆ â„• ] P n âˆ¥
        âˆƒPn = Prop.map
          (Î» (q , qâˆˆupper) â†’
            let (n , nÂ·Îµ>q-p) = archimedesK (q - p) Îµ Îµ>0
                p+nÂ·Îµ>q : p + n â‹† Îµ > q
                p+nÂ·Îµ>q = subst (p + n â‹† Îµ >_) (helper1 p q) (+-lPres< {z = p} nÂ·Îµ>q-p)
            in  n , a .upper-close _ _ qâˆˆupper p+nÂ·Îµ>q)
          (a .upper-inhab)

        interval : Î£[ n âˆˆ â„• ] (Â¬ P n) Ã— P (suc n)
        interval = findInterval isPropP decP Â¬P0 âˆƒPn

      archimedes'' : Î£[ r âˆˆ K ] (Â¬ r âˆˆ a .upper) Ã— (p â‰¤ r) Ã— (r + Îµ) âˆˆ a .upper
      archimedes'' = _ , interval .snd .fst , +-rPosâ†’â‰¥ (nâ‹†qâ‰¥0 (interval .fst) Îµ Îµ>0) ,
        subst (_âˆˆ a .upper)
          ((Î» i â†’ p + sucnâ‹†qâ‰¡nâ‹†q+q (interval .fst) Îµ i) âˆ™ +Assoc _ _ _)
          (interval .snd .snd)


    module _
      (p : K)(Â¬pâˆˆupper : Â¬ p âˆˆ a .upper)
      (q : K)(q<p : q < p)
      where

      private
        min2 : (x y : K) â†’ x > 0r â†’ y > 0r â†’ Î£[ z âˆˆ K ] (z > 0r) Ã— (z < x) Ã— (z < y)
        min2 x y x>0 y>0 = case-split (trichotomy x y)
          where
          case-split : Trichotomy x y â†’ Î£[ z âˆˆ K ] (z > 0r) Ã— (z < x) Ã— (z < y)
          case-split (lt x<y) = middle 0r x , middle>l x>0 , middle<r x>0 , <-trans (middle<r x>0) x<y
          case-split (gt x>y) = middle 0r y , middle>l y>0 , <-trans (middle<r y>0) x>y , middle<r y>0
          case-split (eq xâ‰¡y) =
            middle 0r x , middle>l x>0 , middle<r x>0 , subst (middle 0r x <_) xâ‰¡y (middle<r x>0)

        >-exchange : {a b c : K} â†’ a - b > c â†’ a - c > b
        >-exchange {a = a} {b = b} {c = c} a-b>c =
          transport (Î» i â†’ helper3 a b c i > helper4 b c i) (+-rPres< {z = b - c} a-b>c)

      archimedes''' :
        âˆ¥ Î£[ r âˆˆ K ] Î£[ s âˆˆ K ] (Â¬ s âˆˆ a .upper) Ã— (q < r) Ã— (r < s) Ã— (r + Îµ) âˆˆ a .upper âˆ¥
      archimedes''' =
        let (r , Â¬râˆˆupper , pâ‰¤r , r+Îµâˆˆupper) = archimedes'' p Â¬pâˆˆupper in
        Prop.map
        (Î» (t , t<r+Îµ , tâˆˆupper) â†’
          let r-q = r - q
              r+Îµ-t = (r + Îµ) - t
              r-q>0 : r-q > 0r
              r-q>0 = >â†’Diff>0 (<â‰¤-trans q<p pâ‰¤r)
              r+Îµ-t>0 : r+Îµ-t > 0r
              r+Îµ-t>0 = >â†’Diff>0 t<r+Îµ
              (u , u>0 , u<r-q , u<r+Îµ-t) = min2 r-q r+Îµ-t r-q>0 r+Îµ-t>0
              r+Îµ-u = (r + Îµ) - u
              r-u = r - u
              r-u+Îµ = (r - u) + Îµ
              r-u<r : r-u < r
              r-u<r = +-rNegâ†’< (-Reverse>0 u>0)
              r-u>q : r-u > q
              r-u>q = >-exchange u<r-q
              r-u+Îµ>t : r-u+Îµ > t
              r-u+Îµ>t = subst (_> t) (helper2 r u Îµ) (>-exchange u<r+Îµ-t)
          in  r-u , r , Â¬râˆˆupper , r-u>q , r-u<r , a .upper-close _ _ tâˆˆupper r-u+Îµ>t)
        (a .upper-round _ r+Îµâˆˆupper)


  archimedes' : (a : ð•‚)(Îµ : K)(Îµ>0 : Îµ > 0r)
    â†’ (p : K)  â†’ Î£[ s âˆˆ K ] ((q : K) â†’ q âˆˆ a .upper â†’ s < q) Ã— (p < s)
    â†’ âˆ¥ Î£[ r âˆˆ K ] Î£[ s âˆˆ K ] ((q : K) â†’ q âˆˆ a .upper â†’ s < q) Ã— (p < r) Ã— (r < s) Ã— (r + Îµ) âˆˆ a .upper âˆ¥
  archimedes' a Îµ Îµ>0 p (s , s<qâˆˆupper , p<s) =
    Prop.map
    (Î» (r , s , Â¬sâˆˆupper , q<r , r<s , r+Îµâˆˆupper) â†’
        r , s , Â¬âˆˆupperâ†’<upper a _ Â¬sâˆˆupper , q<r , r<s , r+Îµâˆˆupper)
    (archimedes''' a Îµ Îµ>0 s (<upperâ†’Â¬âˆˆupper a _ s<qâˆˆupper) p p<s)

  archimedes : (a : ð•‚)(Îµ : K)(Îµ>0 : Îµ > 0r)
    â†’ âˆ¥ Î£[ r âˆˆ K ] Î£[ s âˆˆ K ] ((q : K) â†’ q âˆˆ a .upper â†’ s < q) Ã— (r < s) Ã— (r + Îµ) âˆˆ a .upper âˆ¥
  archimedes a Îµ Îµ>0 = Prop.rec squash
    (Î» (q , q<râˆˆupper) â†’ Prop.map
      (Î» (r , s , s<tâˆˆupper , p<r , r<s , r+Îµâˆˆupper) â†’
          r , s , s<tâˆˆupper , r<s , r+Îµâˆˆupper)
      (archimedes' a Îµ Îµ>0 (q - 1r) (q , q<râˆˆupper , q-1<q)))
    (a .lower-inhab)
