{-

Extremum, namely Supremum and Infimum of Subsets

-}
{-# OPTIONS --safe #-}
module Classical.Algebra.OrderedField.Extremum where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels

open import Cubical.Data.Nat using (â„• ; zero ; suc)
open import Cubical.Data.Empty as Empty
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.Relation.Nullary

open import Classical.Preliminary.Logic
open import Classical.Axioms.ExcludedMiddle
open import Classical.Foundations.Powerset
open import Classical.Algebra.OrderedField

private
  variable
    â„“ â„“' : Level


module Extremum (decide : LEM)(ğ’¦ : OrderedField â„“ â„“') where

  open Powerset decide
  open OrderedFieldStr ğ’¦

  private
      K = ğ’¦ .fst .fst .fst

      variable
        p q : K


  {-

    Boundedness of Subsets

  -}

  isUpperBounded : â„™ K â†’ Type (â„“-max â„“ â„“')
  isUpperBounded A = âˆ¥ Î£[ b âˆˆ K ] ((r : K) â†’ r âˆˆ A â†’ r â‰¤ b) âˆ¥

  isLowerBounded : â„™ K â†’ Type (â„“-max â„“ â„“')
  isLowerBounded A = âˆ¥ Î£[ b âˆˆ K ] ((r : K) â†’ r âˆˆ A â†’ b â‰¤ r) âˆ¥


  {-

    Supremum and Infimum of Subsets

  -}

  record Supremum (A : â„™ K) : Type (â„“-max â„“ â„“') where
    field
      sup : K
      bound : (r : K) â†’ r âˆˆ A â†’ r â‰¤ sup
      least : (b : K) â†’ ((r : K) â†’ r âˆˆ A â†’ r â‰¤ b) â†’ b â‰¥ sup

  record Infimum (A : â„™ K) : Type (â„“-max â„“ â„“') where
    field
      inf : K
      bound : (r : K) â†’ r âˆˆ A â†’ inf â‰¤ r
      most  : (b : K) â†’ ((r : K) â†’ r âˆˆ A â†’ b â‰¤ r) â†’ b â‰¤ inf

  open Supremum
  open Infimum


  -- Uniqueness of Extremum

  isPropSupremum : (A : â„™ K) â†’ isProp (Supremum A)
  isPropSupremum A s t i .sup = â‰¤-asym (s .least _ (t .bound)) (t .least _ (s .bound)) i
  isPropSupremum A s t i .bound =
    isPropâ†’PathP (Î» i â†’ isPropÎ 2 (Î» r _ â†’ isPropâ‰¤ {x = r} {y = isPropSupremum A s t i .sup})) (s .bound) (t .bound) i
  isPropSupremum A s t i .least =
    isPropâ†’PathP (Î» i â†’ isPropÎ 2 (Î» b _ â†’ isPropâ‰¤ {x = isPropSupremum A s t i .sup} {y = b})) (s .least) (t .least) i

  isPropInfimum : (A : â„™ K) â†’ isProp (Infimum A)
  isPropInfimum A s t i .inf = â‰¤-asym (t .most _ (s .bound)) (s .most _ (t .bound)) i
  isPropInfimum A s t i .bound =
    isPropâ†’PathP (Î» i â†’ isPropÎ 2 (Î» r _ â†’ isPropâ‰¤ {x = isPropInfimum A s t i .inf} {y = r})) (s .bound) (t .bound) i
  isPropInfimum A s t i .most  =
    isPropâ†’PathP (Î» i â†’ isPropÎ 2 (Î» b _ â†’ isPropâ‰¤ {x = b} {y = isPropInfimum A s t i .inf})) (s .most)  (t .most)  i


  {-

    Basic Properties

  -}

  open ClassicalLogic decide

  <supâ†’âˆƒâˆˆ : {A : â„™ K}(q : K)(boundary : Supremum A) â†’ q < boundary .sup â†’ âˆ¥ Î£[ x âˆˆ K ] (q < x) Ã— (x âˆˆ A) âˆ¥
  <supâ†’âˆƒâˆˆ {A = A} q boundary q<sup with decide (squash {A = Î£[ x âˆˆ K ] (q < x) Ã— (x âˆˆ A)})
  ... | yes p = p
  ... | no Â¬p = Empty.rec (<â‰¤-asym q<sup (boundary .least _ (Î» r râˆˆA â†’ case-split r (trichotomy q r) râˆˆA)))
    where
    case-split : (x : K) â†’ Trichotomy q x â†’ x âˆˆ A â†’ x â‰¤ q
    case-split _ (eq qâ‰¡x) _ = inr (sym qâ‰¡x)
    case-split _ (gt q>x) _ = inl q>x
    case-split x (lt q<x) xâˆˆA = Empty.rec (Â¬âˆƒÃ—â†’âˆ€â†’Â¬ (Î» _ â†’ isProp<) (Î» _ â†’ isPropâˆˆ A) Â¬p x q<x xâˆˆA)

  >supâ†’Â¬âˆˆ : {A : â„™ K}(q : K)(boundary : Supremum A) â†’ q > boundary .sup â†’ Â¬ q âˆˆ A
  >supâ†’Â¬âˆˆ {A = A} q boundary q>sup qâˆˆA with decide (isPropâˆˆ A)
  ... | yes qâˆˆA = <â‰¤-asym q>sup (boundary .bound q qâˆˆA)
  ... | no Â¬qâˆˆA = Â¬qâˆˆA qâˆˆA

  âŠ†â†’supâ‰¤ : {A B : â„™ K} â†’ A âŠ† B â†’ (SupA : Supremum A)(SupB : Supremum B) â†’ SupA .sup â‰¤ SupB .sup
  âŠ†â†’supâ‰¤ {A = A} {B = B} AâŠ†B SupA SupB = SupA .least _ (Î» r râˆˆA â†’ SupB .bound r (AâŠ†B râˆˆA))


  >infâ†’âˆƒâˆˆ : {A : â„™ K}(q : K)(boundary : Infimum A) â†’ q > boundary .inf â†’ âˆ¥ Î£[ x âˆˆ K ] (x < q) Ã— (x âˆˆ A) âˆ¥
  >infâ†’âˆƒâˆˆ {A = A} q boundary q>inf with decide (squash {A = Î£[ x âˆˆ K ] (x < q) Ã— (x âˆˆ A)})
  ... | yes p = p
  ... | no Â¬p = Empty.rec (<â‰¤-asym q>inf (boundary .most _ (Î» r râˆˆA â†’ case-split r (trichotomy q r) râˆˆA)))
    where
    case-split : (x : K) â†’ Trichotomy q x â†’ x âˆˆ A â†’ q â‰¤ x
    case-split _ (eq qâ‰¡x) _ = inr qâ‰¡x
    case-split _ (lt q<x) _ = inl q<x
    case-split x (gt q>x) xâˆˆA = Empty.rec (Â¬âˆƒÃ—â†’âˆ€â†’Â¬ (Î» _ â†’ isProp<) (Î» _ â†’ isPropâˆˆ A) Â¬p x q>x xâˆˆA)

  <infâ†’Â¬âˆˆ : {A : â„™ K}(q : K)(boundary : Infimum A) â†’ q < boundary .inf â†’ Â¬ q âˆˆ A
  <infâ†’Â¬âˆˆ {A = A} q boundary q<inf qâˆˆA with decide (isPropâˆˆ A)
  ... | yes qâˆˆA = <â‰¤-asym q<inf (boundary .bound q qâˆˆA)
  ... | no Â¬qâˆˆA = Â¬qâˆˆA qâˆˆA


  {-

    Taking - x for all x âˆˆ some subset and reverse its extremum

  -}

  module _ (A : â„™ K) where

    -prop : K â†’ hProp _
    -prop x = - x âˆˆ A , isPropâˆˆ A

    -â„™ : â„™ K
    -â„™ = specify -prop


  xâˆˆAâ†’-xâˆˆ-A : (A : â„™ K){x : K} â†’ x âˆˆ A â†’ - x âˆˆ -â„™ A
  xâˆˆAâ†’-xâˆˆ-A A {x = x} xâˆˆA = Inhabâ†’âˆˆ (-prop A) (subst (_âˆˆ A) (sym (-Idempotent x)) xâˆˆA)

  -â„™-Idem : (A : â„™ K) â†’ -â„™ (-â„™ A) â‰¡ A
  -â„™-Idem A = biâŠ†â†’â‰¡ âŠ†-helper âŠ‡helper
    where
    âŠ†-helper : {x : K} â†’ x âˆˆ -â„™ (-â„™ A) â†’ x âˆˆ A
    âŠ†-helper {x = x} xâˆˆ =
      subst (_âˆˆ A) (-Idempotent x) (âˆˆâ†’Inhab (-prop A) (âˆˆâ†’Inhab (-prop (-â„™ A)) xâˆˆ))

    âŠ‡helper : {x : K} â†’ x âˆˆ A â†’ x âˆˆ -â„™ (-â„™ A)
    âŠ‡helper {x = x} xâˆˆ =
      Inhabâ†’âˆˆ (-prop (-â„™ A)) (Inhabâ†’âˆˆ (-prop A) (subst (_âˆˆ A) (sym (-Idempotent x)) xâˆˆ))


  isInhabited- : (A : â„™ K) â†’ isInhabited A â†’ isInhabited (-â„™ A)
  isInhabited- A = Prop.map (Î» (x , xâˆˆA) â†’ - x , xâˆˆAâ†’-xâˆˆ-A A xâˆˆA)


  isUpperBoundedâ†’isLowerBounded : (A : â„™ K) â†’ isUpperBounded A â†’ isLowerBounded (-â„™ A)
  isUpperBoundedâ†’isLowerBounded A =
    Prop.map (Î» (b , bâ‰¥râˆˆA) â†’ - b , (Î» r râˆˆ-A â†’ -lReverseâ‰¤ (bâ‰¥râˆˆA _ (âˆˆâ†’Inhab (-prop A) râˆˆ-A))))

  isLowerBoundedâ†’isUpperBounded : (A : â„™ K) â†’ isLowerBounded A â†’ isUpperBounded (-â„™ A)
  isLowerBoundedâ†’isUpperBounded A =
    Prop.map (Î» (b , bâ‰¤râˆˆA) â†’ - b , (Î» r râˆˆ-A â†’ -rReverseâ‰¤ (bâ‰¤râˆˆA _ (âˆˆâ†’Inhab (-prop A) râˆˆ-A))))


  Supâ†’Inf- : (A : â„™ K) â†’ Supremum A â†’ Infimum (-â„™ A)
  Supâ†’Inf- A Sup .inf = - Sup .sup
  Supâ†’Inf- A Sup .bound r râˆˆ-A = -lReverseâ‰¤ (Sup .bound _ (âˆˆâ†’Inhab (-prop A) râˆˆ-A))
  Supâ†’Inf- A Sup .most  b bâ‰¤râˆˆ-A = -rReverseâ‰¤ (Sup .least _ -bâ‰¥râˆˆ-A)
    where
    -bâ‰¥râˆˆ-A : (r : K) â†’ r âˆˆ A â†’ - b â‰¥ r
    -bâ‰¥râˆˆ-A r râˆˆA = -rReverseâ‰¤ (bâ‰¤râˆˆ-A _ (xâˆˆAâ†’-xâˆˆ-A A râˆˆA))

  Infâ†’Sup- : (A : â„™ K) â†’ Infimum A â†’ Supremum (-â„™ A)
  Infâ†’Sup- A Inf .sup = - Inf .inf
  Infâ†’Sup- A Inf .bound r râˆˆ-A = -rReverseâ‰¤ (Inf .bound _ (âˆˆâ†’Inhab (-prop A) râˆˆ-A))
  Infâ†’Sup- A Inf .least b bâ‰¥râˆˆ-A = -lReverseâ‰¤ (Inf .most  _ -bâ‰¤râˆˆ-A)
    where
    -bâ‰¤râˆˆ-A : (r : K) â†’ r âˆˆ A â†’ - b â‰¤ r
    -bâ‰¤râˆˆ-A r râˆˆA = -lReverseâ‰¤ (bâ‰¥râˆˆ-A _ (xâˆˆAâ†’-xâˆˆ-A A râˆˆA))

  Supâ†’Inf : (A : â„™ K) â†’ Supremum (-â„™ A) â†’ Infimum A
  Supâ†’Inf A Sup = subst Infimum (-â„™-Idem A) (Supâ†’Inf- _ Sup)

  Infâ†’Sup : (A : â„™ K) â†’ Infimum (-â„™ A) â†’ Supremum A
  Infâ†’Sup A Sup = subst Supremum (-â„™-Idem A) (Infâ†’Sup- _ Sup)
