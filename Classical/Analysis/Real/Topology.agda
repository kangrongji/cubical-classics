{-

Topology of Real Numbers

This file contains:
- The canonical metric of â„ and its induced topology;
- Basics of closed interval;
- Basics of bounded subset of â„;
- The Heine-Borel theorem.

-}
{-# OPTIONS --safe #-}
module Classical.Analysis.Real.Topology where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.Data.Empty as Empty
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.Relation.Nullary

open import Classical.Axioms.ExcludedMiddle
open import Classical.Foundations.Powerset

open import Classical.Algebra.OrderedRing.AbsoluteValue
open import Classical.Algebra.OrderedField
open import Classical.Algebra.OrderedField.Extremum
open import Classical.Algebra.OrderedField.Completeness
open import Classical.Analysis.Real.Base

open import Classical.Topology.Base
open import Classical.Topology.Properties
open import Classical.Topology.Hausdorff
open import Classical.Topology.Metric


module TopologyOfReal (decide : LEM) where

  open Powerset decide renaming ([_] to [[_]])
  open Real     decide
  open AbsoluteValue   (â„CompleteOrderedField .fst .fst)
  open OrderedFieldStr (â„CompleteOrderedField .fst)
  open MetricStr   decide
  open TopologyStr decide
  open TopologyProperties decide
  open Hausdorff   decide
  open Topology
  open Metric

  private
    variable
      x y z : â„


  {-

    Canonical Metric and Topology of â„

  -}

  -- â„ is a metric space

  instance

    â„Metric : Metric â„
    â„Metric .dist x y = abs (x - y)
    â„Metric .dist-id   dâ‰¡0 = diffâ‰¡0â†’xâ‰¡y (absâ‰¡0â†’xâ‰¡0 dâ‰¡0)
    â„Metric .dist-refl xâ‰¡y = xâ‰¡0â†’absâ‰¡0 (xâ‰¡yâ†’diffâ‰¡0 xâ‰¡y)
    â„Metric .dist-symm _ _ = absxâ‰¡â†’abs-x âˆ™ cong abs (sym x-yâ‰¡-[y-x])
    â„Metric .dist-Î” _ _ _ = Î”-Inequality

    â„Topology : Topology â„
    â„Topology = Metricâ†’Topology â¦ƒ â„Metric â¦„


  -- â„ is Hausdorff space

  isHausdorffâ„ : isHausdorff â¦ƒ â„Topology â¦„
  isHausdorffâ„ = isHausdorffMetric


  {-

    Closed Interval

  -}

  module _ (a b : â„) â¦ƒ aâ‰¤b : a â‰¤ b â¦„ where

    ğˆ-prop : â„ â†’ hProp _
    ğˆ-prop x = (a â‰¤ x) Ã— (x â‰¤ b) , isPropÃ— isPropâ‰¤ isPropâ‰¤

    [_,_] : â„™ â„
    [_,_] = specify ğˆ-prop

    instance
      aâˆˆğˆ : a âˆˆ [_,_]
      aâˆˆğˆ = Inhabâ†’âˆˆ ğˆ-prop (inr refl , aâ‰¤b)

      bâˆˆğˆ : b âˆˆ [_,_]
      bâˆˆğˆ = Inhabâ†’âˆˆ ğˆ-prop (aâ‰¤b , inr refl)


  module _ {a b : â„} â¦ƒ aâ‰¤b : a â‰¤ b â¦„ where

    Inhabâ†’âˆˆğˆ : a â‰¤ x â†’ x â‰¤ b â†’ x âˆˆ [ a , b ]
    Inhabâ†’âˆˆğˆ aâ‰¤x xâ‰¤b = Inhabâ†’âˆˆ (ğˆ-prop a b) (aâ‰¤x , xâ‰¤b)

    âˆˆâ†’Inhabğˆ-L : x âˆˆ [ a , b ] â†’ a â‰¤ x
    âˆˆâ†’Inhabğˆ-L xâˆˆğˆ = âˆˆâ†’Inhab (ğˆ-prop a b) xâˆˆğˆ .fst

    âˆˆâ†’Inhabğˆ-R : x âˆˆ [ a , b ] â†’ x â‰¤ b
    âˆˆâ†’Inhabğˆ-R xâˆˆğˆ = âˆˆâ†’Inhab (ğˆ-prop a b) xâˆˆğˆ .snd

    xâˆˆ[a,b] : a â‰¡ b â†’ x âˆˆ [ a , b ] â†’ a â‰¡ x
    xâˆˆ[a,b] {x = x} aâ‰¡b xâˆˆğˆ = â‰¤-asym (âˆˆâ†’Inhabğˆ-L xâˆˆğˆ) (subst (x â‰¤_) (sym aâ‰¡b) (âˆˆâ†’Inhabğˆ-R xâˆˆğˆ))


  instance
    _ : 0 â‰¤ 1
    _ = inl 1>0

  Unitğˆ : â„™ â„
  Unitğˆ = [ 0 , 1 ]


  module _ (a b : â„) â¦ƒ aâ‰¤b : a â‰¤ b â¦„ where

    -- Closed interval is compact.

    isCompactInterval : isCompactSub [ a , b ]
    isCompactInterval = cov[a,b]
      where

      module _ {ğ’° : â„™ â„™ â„}(ğ’°covğˆ : ğ’° covers [ a , b ]) where

        open Extremum decide (â„CompleteOrderedField .fst)
        open Supremum

        getSup = â„CompleteOrderedField .snd

        cov-prop : â„ â†’ hProp _
        cov-prop x =
          (Î£[ xâˆˆğˆ âˆˆ x âˆˆ [ a , b ] ]
            âˆ¥ Î£[ ğ’°â‚€ âˆˆ â„™ (â„™ â„) ] ğ’°â‚€ âŠ† ğ’° Ã— isFinSub ğ’°â‚€ Ã— ğ’°â‚€ covers [ a , x ] â¦ƒ âˆˆâ†’Inhabğˆ-L xâˆˆğˆ â¦„ âˆ¥) ,
          isPropÎ£ (isPropâˆˆ [ a , b ]) (Î» _ â†’ squash)

        cov-sub = specify cov-prop

        aâ‰¤xâˆˆsub : (x : â„) â†’ x âˆˆ cov-sub â†’ x â‰¥ a
        aâ‰¤xâˆˆsub x xâˆˆsub = âˆˆâ†’Inhabğˆ-L (âˆˆâ†’Inhab cov-prop xâˆˆsub .fst)

        bâ‰¥xâˆˆsub : (x : â„) â†’ x âˆˆ cov-sub â†’ x â‰¤ b
        bâ‰¥xâˆˆsub x xâˆˆsub =  âˆˆâ†’Inhabğˆ-R (âˆˆâ†’Inhab cov-prop xâˆˆsub .fst)

        instance
          aâ‰¤a : a â‰¤ a
          aâ‰¤a = inr refl

        cov-sup : Supremum cov-sub
        cov-sup = getSup âˆ£ a , Inhabâ†’âˆˆ cov-prop (aâˆˆğˆ a b , cov-a) âˆ£ âˆ£ b , bâ‰¥xâˆˆsub âˆ£
          where
          cov-a : âˆ¥ Î£[ ğ’°â‚€ âˆˆ â„™ (â„™ â„) ] ğ’°â‚€ âŠ† ğ’° Ã— isFinSub ğ’°â‚€ Ã— ğ’°â‚€ covers [ a , a ] â¦ƒ âˆˆâ†’Inhabğˆ-L (aâˆˆğˆ a b) â¦„ âˆ¥
          cov-a = Prop.map
            (Î» (U , aâˆˆU , Uâˆˆğ’°) â†’
              [[ U ]] , AâˆˆSâ†’[A]âŠ†S Uâˆˆğ’° , isFinSub[x] ,
              (Î» {x} xâˆˆ[a,a] â†’
                let aâ‰¡x : a â‰¡ x
                    aâ‰¡x = xâˆˆ[a,b] refl xâˆˆ[a,a]
                in  subst (x âˆˆ_) (sym union[A]) (subst (_âˆˆ U) aâ‰¡x aâˆˆU)) ,
              AâˆˆSâ†’[A]âŠ†S (ğ’°covğˆ .snd Uâˆˆğ’°))
            (âˆˆcover (aâˆˆğˆ a b) ğ’°covğˆ)

        xâ‚€ = cov-sup .sup

        instance
          xâ‚€â‰¥a : xâ‚€ â‰¥ a
          xâ‚€â‰¥a = supLowerBounded _ cov-sup aâ‰¤xâˆˆsub

        xâ‚€â‰¤b : xâ‚€ â‰¤ b
        xâ‚€â‰¤b = supUpperBounded _ cov-sup bâ‰¥xâˆˆsub

        xâ‚€âˆˆğˆ : xâ‚€ âˆˆ [ a , b ]
        xâ‚€âˆˆğˆ = Inhabâ†’âˆˆğˆ xâ‚€â‰¥a xâ‚€â‰¤b

        module _
          (U : â„™ â„)(r : â„) â¦ƒ r>0 : r > 0 â¦„ (Uâˆˆğ’° : U âˆˆ ğ’°)(â„¬xâ‚€râŠ†U : â„¬ xâ‚€ r âŠ† U)
          (y : â„)(xâ‚€-r<y : xâ‚€ - r < y)(yâˆˆsub : y âˆˆ cov-sub) where

          instance
            aâ‰¤y : a â‰¤ y
            aâ‰¤y = aâ‰¤xâˆˆsub y yâˆˆsub

          module _
            (ğ’°â‚€ : â„™ â„™ â„)(ğ’°â‚€âŠ†ğ’° : ğ’°â‚€ âŠ† ğ’°)(finğ’°â‚€ : isFinSub ğ’°â‚€)(cov : ğ’°â‚€ covers [ a , y ])
            where

            ğ’°â‚€+U : â„™ â„™ â„
            ğ’°â‚€+U = ğ’°â‚€ âˆª [[ U ]]

            ğ’°â‚€+Uâˆˆğ’° : ğ’°â‚€+U âŠ† ğ’°
            ğ’°â‚€+Uâˆˆğ’° = âŠ†â†’âŠ†âˆª {C = ğ’°} ğ’°â‚€âŠ†ğ’° (AâˆˆSâ†’[A]âŠ†S {S = ğ’°} Uâˆˆğ’°)

            finğ’°â‚€+U : isFinSub ğ’°â‚€+U
            finğ’°â‚€+U = isfinsuc finğ’°â‚€ U

            âˆª-helper : {x : â„} â†’ (x âˆˆ union ğ’°â‚€) âŠ (x âˆˆ U) â†’ x âˆˆ union ğ’°â‚€+U
            âˆª-helper (inl xâˆˆâˆªğ’°â‚€) = unionâˆª-leftâŠ† xâˆˆâˆªğ’°â‚€
            âˆª-helper {x = x} (inr xâˆˆ[U]) = unionâˆª-rightâŠ† (subst (x âˆˆ_) (sym union[A]) xâˆˆ[U])

            covSup : ğ’°â‚€+U covers [ a , xâ‚€ ]
            covSup .fst {x = x} xâˆˆ[a,xâ‚€] = case-split (<â‰¤-total y x)
              where
              case-split : _ â†’ _
              case-split (inl x>y) = âˆª-helper (inr (â„¬xâ‚€râŠ†U xâˆˆâ„¬xâ‚€r))
                where
                xâˆˆâ„¬xâ‚€r : x âˆˆ â„¬ xâ‚€ r
                xâˆˆâ„¬xâ‚€r = Inhabâ†’âˆˆâ„¬ (absInBetween<â‰¤ r>0 (<-trans xâ‚€-r<y x>y) (âˆˆâ†’Inhabğˆ-R xâˆˆ[a,xâ‚€]))
              case-split (inr xâ‰¤y) = âˆª-helper (inl (cov .fst xâˆˆ[a,y]))
                where
                xâˆˆ[a,y] : x âˆˆ [ a , y ]
                xâˆˆ[a,y] = Inhabâ†’âˆˆğˆ (âˆˆâ†’Inhabğˆ-L xâˆˆ[a,xâ‚€]) xâ‰¤y
            covSup .snd = âŠ†-trans {A = ğ’°â‚€+U} ğ’°â‚€+Uâˆˆğ’° (ğ’°covğˆ .snd)

            xâ‚€âˆˆcov : xâ‚€ âˆˆ cov-sub
            xâ‚€âˆˆcov = Inhabâ†’âˆˆ cov-prop (xâ‚€âˆˆğˆ , âˆ£ ğ’°â‚€+U , ğ’°â‚€+Uâˆˆğ’° , finğ’°â‚€+U , covSup âˆ£)

            module _ (xâ‚€<b : xâ‚€ < b) where

              Îµ-triple = min2 {y = b - xâ‚€} r>0 (>â†’Diff>0 xâ‚€<b)

              Îµ = Îµ-triple .fst
              Îµ>0 = Îµ-triple .snd .fst
              Îµ<r = Îµ-triple .snd .snd .fst
              Îµ<b-xâ‚€ = Îµ-triple .snd .snd .snd

              instance
                aâ‰¤xâ‚€+Îµ : a â‰¤ xâ‚€ + Îµ
                aâ‰¤xâ‚€+Îµ = inl (â‰¤<-trans xâ‚€â‰¥a (+-rPosâ†’> Îµ>0))

              xâ‚€+Îµâˆˆğˆ : (xâ‚€ + Îµ) âˆˆ [ a , b ]
              xâ‚€+Îµâˆˆğˆ = Inhabâ†’âˆˆğˆ aâ‰¤xâ‚€+Îµ (inl (-MoveRToL<' Îµ<b-xâ‚€))

              covMore : ğ’°â‚€+U covers [ a , xâ‚€ + Îµ ]
              covMore .fst {x = x} xâˆˆ[a,xâ‚€+Îµ] = case-split (<â‰¤-total y x)
                where
                case-split : _ â†’ _
                case-split (inl x>y) = âˆª-helper (inr (â„¬xâ‚€râŠ†U xâˆˆâ„¬xâ‚€r))
                  where
                  xâˆˆâ„¬xâ‚€r : x âˆˆ â„¬ xâ‚€ r
                  xâˆˆâ„¬xâ‚€r = Inhabâ†’âˆˆâ„¬ (absInOpenInterval r>0 (<-trans xâ‚€-r<y x>y)
                    (â‰¤<-trans (âˆˆâ†’Inhabğˆ-R xâˆˆ[a,xâ‚€+Îµ]) (+-lPres< Îµ<r)))
                case-split (inr xâ‰¤y) = âˆª-helper (inl (cov .fst xâˆˆ[a,y]))
                  where
                  xâˆˆ[a,y] : x âˆˆ [ a , y ]
                  xâˆˆ[a,y] = Inhabâ†’âˆˆğˆ (âˆˆâ†’Inhabğˆ-L xâˆˆ[a,xâ‚€+Îµ]) xâ‰¤y
              covMore .snd = âŠ†-trans {A = ğ’°â‚€+U} ğ’°â‚€+Uâˆˆğ’° (ğ’°covğˆ .snd)

              no-way : âŠ¥
              no-way = <â‰¤-asym (+-rPosâ†’> Îµ>0) (cov-sup .bound _ xâ‚€+Îµâˆˆcov)
                where
                xâ‚€+Îµâˆˆcov : (xâ‚€ + Îµ) âˆˆ cov-sub
                xâ‚€+Îµâˆˆcov = Inhabâ†’âˆˆ cov-prop (xâ‚€+Îµâˆˆğˆ , âˆ£ ğ’°â‚€+U , ğ’°â‚€+Uâˆˆğ’° , finğ’°â‚€+U , covMore âˆ£)

            xâ‚€âˆˆcovÃ—Â¬xâ‚€<b' : (xâ‚€ âˆˆ cov-sub) Ã— (Â¬ xâ‚€ < b)
            xâ‚€âˆˆcovÃ—Â¬xâ‚€<b' = xâ‚€âˆˆcov , no-way

        âˆƒâ„¬ : âˆ¥ Î£[ U âˆˆ â„™ â„ ] Î£[ r âˆˆ â„ ] Î£[ r>0 âˆˆ r > 0 ] (U âˆˆ ğ’°) Ã— (â„¬ xâ‚€ r â¦ƒ r>0 â¦„ âŠ† U) âˆ¥
        âˆƒâ„¬ = Prop.rec squash
          (Î» (U , xâ‚€âˆˆU , Uâˆˆğ’°) â†’ Prop.map
          (Î» (r , r>0 , â„¬xrâŠ†U) â†’ U , r , r>0 , Uâˆˆğ’° , (Î» p â†’ â„¬xrâŠ†U p))
          (âˆˆâ†’Inhabğ“‚ (ğ’°covğˆ .snd Uâˆˆğ’°) xâ‚€ xâ‚€âˆˆU))
          (âˆˆcover xâ‚€âˆˆğˆ ğ’°covğˆ)

        isPropÃ—' : isProp ((xâ‚€ âˆˆ cov-sub) Ã— (Â¬ xâ‚€ < b))
        isPropÃ—' = isPropÃ— (isPropâˆˆ cov-sub) (isPropÂ¬ _)

        xâ‚€âˆˆcovÃ—Â¬xâ‚€<b : (xâ‚€ âˆˆ cov-sub) Ã— (Â¬ xâ‚€ < b)
        xâ‚€âˆˆcovÃ—Â¬xâ‚€<b = Prop.rec isPropÃ—'
          (Î» (U , r , r>0 , Uâˆˆğ’° , â„¬xrâŠ†U) â†’ Prop.rec isPropÃ—'
          (Î» (y , xâ‚€-r<y , yâˆˆsub) â†’ Prop.rec isPropÃ—'
          (Î» (ğ’°â‚€ , ğ’°â‚€âŠ†ğ’° , finğ’°â‚€ , cov) â†’
            xâ‚€âˆˆcovÃ—Â¬xâ‚€<b'
              U r â¦ƒ r>0 â¦„ Uâˆˆğ’° â„¬xrâŠ†U
              y xâ‚€-r<y yâˆˆsub
              ğ’°â‚€ ğ’°â‚€âŠ†ğ’° finğ’°â‚€ cov)
          (âˆˆâ†’Inhab cov-prop yâˆˆsub .snd))
          (<supâ†’âˆƒâˆˆ (xâ‚€ - r) cov-sup (-rPosâ†’< r>0))) âˆƒâ„¬

        xâ‚€â‰¡b : xâ‚€ â‰¡ b
        xâ‚€â‰¡b = â‰¤+Â¬<â†’â‰¡ xâ‚€â‰¤b (xâ‚€âˆˆcovÃ—Â¬xâ‚€<b .snd)

        cov[a,b]' : cov-prop b .fst
        cov[a,b]' = âˆˆâ†’Inhab cov-prop (subst (_âˆˆ cov-sub) xâ‚€â‰¡b (xâ‚€âˆˆcovÃ—Â¬xâ‚€<b .fst))

        cov[a,b] : âˆ¥ Î£[ ğ’°â‚€ âˆˆ â„™ (â„™ â„) ] ğ’°â‚€ âŠ† ğ’° Ã— isFinSub ğ’°â‚€ Ã— ğ’°â‚€ covers [ a , b ] âˆ¥
        cov[a,b] = cov[a,b]' .snd


    -- Closed interval is closed.

    isClosedInterval : isClosedSub [ a , b ]
    isClosedInterval = isCompactSubâ†’isClosedSub isHausdorffâ„ isCompactInterval


  {-

    Bounded Subset

  -}


  -- Two usual formulations of boundedness, and they are equivalent.

  isBoundedSub : â„™ â„ â†’ Type
  isBoundedSub A = âˆ¥ Î£[ a âˆˆ â„ ] Î£[ b âˆˆ â„ ] (a â‰¤ b) Ã— ((x : â„) â†’ x âˆˆ A â†’ (a â‰¤ x) Ã— (x â‰¤ b)) âˆ¥

  isBoundedByInterval : â„™ â„ â†’ Type
  isBoundedByInterval A = âˆ¥ Î£[ a âˆˆ â„ ] Î£[ b âˆˆ â„ ] Î£[ aâ‰¤b âˆˆ a â‰¤ b ] A âŠ† [ a , b ] â¦ƒ aâ‰¤b â¦„ âˆ¥

  isBoundedSubâ†’isBoundedByInterval : {A : â„™ â„} â†’ isBoundedSub A â†’ isBoundedByInterval A
  isBoundedSubâ†’isBoundedByInterval =
    Prop.map (Î» (a , b , aâ‰¤b , h) â†’
      a , b , aâ‰¤b , Î» {x} xâˆˆA â†’ Inhabâ†’âˆˆğˆ â¦ƒ aâ‰¤b â¦„ (h x xâˆˆA .fst) (h x xâˆˆA .snd))

  isBoundedByIntervalâ†’isBoundedSub : {A : â„™ â„} â†’ isBoundedByInterval A â†’ isBoundedSub A
  isBoundedByIntervalâ†’isBoundedSub =
    Prop.map (Î» (a , b , aâ‰¤b , AâŠ†ğˆ) â†’
      a , b , aâ‰¤b , Î» x xâˆˆA â†’ âˆˆâ†’Inhabğˆ-L â¦ƒ aâ‰¤b â¦„ (AâŠ†ğˆ xâˆˆA) , âˆˆâ†’Inhabğˆ-R â¦ƒ aâ‰¤b â¦„ (AâŠ†ğˆ xâˆˆA))


  {-

    Heine-Borel Theorem

  -}

  -- A bounded and closed subset of â„ is compact under the canonical topology.

  isBoundedClosedSubâ†’isCompactSub : {A : â„™ â„} â†’ isBoundedSub A â†’ isClosedSub A â†’ isCompactSub A
  isBoundedClosedSubâ†’isCompactSub {A = A} bA cA =
    Prop.rec isPropIsCompactSub
    (Î» (a , b , aâ‰¤b , AâŠ†ğˆ) â†’
      isClosedInCompactâ†’isCompact AâŠ†ğˆ cA ( isCompactInterval a b â¦ƒ aâ‰¤b â¦„))
    (isBoundedSubâ†’isBoundedByInterval bA)
