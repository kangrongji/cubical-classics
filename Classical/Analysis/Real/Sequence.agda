{-

Sequence of Real Numbers

This file contains:
- Basics of real-number sequence;
- The monotone convergence theorem;
- The Bolzano-Weierstrass theorem;
- The Cauchy completeness of â„.

-}
{-# OPTIONS --safe #-}
module Classical.Analysis.Real.Sequence where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Data.Nat
  using    (â„• ; suc ; zero)
  renaming (_+_ to _+â„•_)
open import Cubical.Data.Nat.Order
  using    (<-weaken ; â‰¤0â†’â‰¡0)
  renaming (_>_ to _>â„•_ ; _<_ to _<â„•_
          ; _â‰¥_ to _â‰¥â„•_ ; _â‰¤_ to _â‰¤â„•_
          ; isPropâ‰¤  to isPropâ‰¤â„•
          ; â‰¤-refl   to â‰¤â„•-refl
          ; <-trans  to <â„•-trans
          ; <â‰¤-trans to <â‰¤â„•-trans
          ; â‰¤<-trans to â‰¤<â„•-trans)
open import Cubical.Data.Empty as Empty
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.HITs.PropositionalTruncation.Monad
open import Cubical.Relation.Nullary

open import Classical.Axioms
open import Classical.Foundations.Powerset
open import Classical.Preliminary.Nat
open import Classical.Preliminary.Logic
open import Classical.Algebra.OrderedRing.AbsoluteValue
open import Classical.Algebra.OrderedField
open import Classical.Algebra.OrderedField.Extremum
open import Classical.Algebra.OrderedField.Completeness
open import Classical.Topology.Metric
open import Classical.Topology.Metric.Sequence
open import Classical.Analysis.Real.Base
open import Classical.Analysis.Real.Topology


module _ â¦ƒ ğŸ¤– : Oracle â¦„ where

  open Oracle ğŸ¤–

  open OrderedFieldStr (â„CompleteOrderedField .fst)
  open AbsoluteValue   (â„CompleteOrderedField .fst .fst)
  open Metric   â„Metric

  open CompleteOrderedField (â„CompleteOrderedField .fst)
  open Extremum        (â„CompleteOrderedField .fst)
  open Supremum

  open Limit
  open ClusterPoint

  private
    getSup = â„CompleteOrderedField .snd


  {-

    Monotone Convergence Theorem

  -}

  -- Monotone increasing and upper-bounded sequence

  isIncreasing : (â„• â†’ â„) â†’ Type
  isIncreasing seq = (m n : â„•) â†’ m â‰¥â„• n â†’ seq m â‰¥ seq n

  isUpperBoundedSequence : (â„• â†’ â„) â†’ Type
  isUpperBoundedSequence seq = âˆ¥ Î£[ b âˆˆ â„ ] ((n : â„•) â†’ seq n â‰¤ b) âˆ¥â‚


  -- A weaker formulation of incresing, and their equivalence

  isIncreasing' : (â„• â†’ â„) â†’ Type
  isIncreasing' seq = (n : â„•) â†’ seq (suc n) â‰¥ seq n

  isIncreasing'â†’isIncreasing : {seq : â„• â†’ â„} â†’ isIncreasing' seq â†’ isIncreasing seq
  isIncreasing'â†’isIncreasing {seq = seq} incr m n mâ‰¥n = â‰¥-helper m (mâ‰¥n .fst) (mâ‰¥n .snd)
    where
    â‰¥-helper : (m k : â„•) â†’ k +â„• n â‰¡ m â†’ seq m â‰¥ seq n
    â‰¥-helper m 0 nâ‰¡m  = subst (Î» x â†’ seq x â‰¥ seq n) nâ‰¡m  (â‰¤-refl refl)
    â‰¥-helper m 1 snâ‰¡m = subst (Î» x â†’ seq x â‰¥ seq n) snâ‰¡m (incr n)
    â‰¥-helper m (suc (suc k)) ssk+nâ‰¡m = subst (Î» x â†’ seq x â‰¥ seq n) ssk+nâ‰¡m
        (â‰¤-trans (â‰¥-helper _ (suc k) refl) (incr _))


  -- Monotone increasing and upper-bounded sequence has a limit.

  isMonoBoundedâ†’Limit : {seq : â„• â†’ â„} â†’ isIncreasing seq â†’ isUpperBoundedSequence seq â†’ Limit seq
  isMonoBoundedâ†’Limit {seq = seq} incr boundSeq =
    record { lim = limit ; conv = Î» Îµ Îµ>0 â†’ âˆ£ nâ‚€ Îµ Îµ>0 , converge Îµ Îµ>0 âˆ£â‚ }
    where

    seq-prop : â„ â†’ hProp _
    seq-prop x = âˆ¥ Î£[ n âˆˆ â„• ] seq n â‰¡ x âˆ¥â‚ , squashâ‚

    seq-sub : â„™ â„
    seq-sub = specify seq-prop

    boundSub : isUpperBounded seq-sub
    boundSub = do
      (b , seqnâ‰¤b) â† boundSeq
      return (b , Î» r râˆˆsub â†’
        proof _ , isPropâ‰¤ by do
        (n , seqnâ‰¡r) â† âˆˆâ†’Inhab seq-prop râˆˆsub
        return (subst (_â‰¤ b) seqnâ‰¡r (seqnâ‰¤b n)))

    seq-sup : Supremum seq-sub
    seq-sup = getSup âˆ£ _ , Inhabâ†’âˆˆ seq-prop âˆ£ 0 , refl âˆ£â‚ âˆ£â‚ boundSub

    limit : â„
    limit = seq-sup .sup

    lim-seqnâ‰¥0 : (n : â„•) â†’ limit - seq n â‰¥ 0
    lim-seqnâ‰¥0 n = â‰¥â†’Diffâ‰¥0 (seq-sup .bound _ (Inhabâ†’âˆˆ seq-prop âˆ£ _ , refl âˆ£â‚))

    module _ (Îµ : â„)(Îµ>0 : Îµ > 0) where

      âˆƒp : âˆ¥ Î£[ n âˆˆ â„• ] (limit - seq n < Îµ) âˆ¥â‚
      âˆƒp = do
        (x , lim-Îµ<x , xâˆˆsub) â† <supâ†’âˆƒâˆˆ _ seq-sup (-rPosâ†’< Îµ>0)
        (n , seqnâ‰¡x) â† âˆˆâ†’Inhab seq-prop xâˆˆsub
        let lim-Îµ<seqn : limit - Îµ < seq n
            lim-Îµ<seqn = subst (limit - Îµ <_) (sym seqnâ‰¡x) lim-Îµ<x
            lim-seqn<Îµ : limit - seq n < Îµ
            lim-seqn<Îµ = +-MoveRToL<' (-MoveLToR< lim-Îµ<seqn)
        return (n , lim-seqn<Îµ)

      Î£p : Î£[ n âˆˆ â„• ] limit - seq n < Îµ
      Î£p = findByOracle (Î» _ â†’ isProp<) âˆƒp

      nâ‚€ = Î£p .fst

      converge : (n : â„•) â†’ n >â„• nâ‚€ â†’ abs (limit - seq n) < Îµ
      converge n n>nâ‚€ =
        subst (_< Îµ) (sym (xâ‰¥0â†’absâ‰¡x (lim-seqnâ‰¥0 n))) lim-seqn<Îµ
        where
        lim-seqn<Îµ : limit - seq n < Îµ
        lim-seqn<Îµ = â‰¤<-trans (+-lPresâ‰¤ (-Reverseâ‰¤ (incr _ _ (<-weaken n>nâ‚€)))) (Î£p .snd)


  {-

    The Bolzano-Weierstrass Theorem

  -}

  -- Bounded sequence

  isBoundedSequence : (â„• â†’ â„) â†’ Type
  isBoundedSequence seq = âˆ¥ Î£[ a âˆˆ â„ ] Î£[ b âˆˆ â„ ] ((n : â„•) â†’ (a â‰¤ seq n) Ã— (seq n â‰¤ b)) âˆ¥â‚


  -- Sequence of real numbers admits cluster point when it is bounded.

  isBoundedâ†’ClusterPoint : {seq : â„• â†’ â„} â†’ isBoundedSequence seq â†’ ClusterPoint seq
  isBoundedâ†’ClusterPoint {seq = seq} bSeq = record { point = xâ‚€ ; accum = âˆƒcluster }
    where

    accum-prop : â„ â†’ hProp _
    accum-prop x = ((n : â„•) â†’ âˆ¥ Î£[ n' âˆˆ â„• ] (n â‰¤â„• n') Ã— (x â‰¤ seq n') âˆ¥â‚) ,
      isPropÎ  (Î» _ â†’ squashâ‚)

    accum-sub = specify accum-prop

    module _
      ((a , b , bound) : Î£[ a âˆˆ â„ ] Î£[ b âˆˆ â„ ] ((n : â„•) â†’ (a â‰¤ seq n) Ã— (seq n â‰¤ b)))
      where

      aâˆˆaccum : a âˆˆ accum-sub
      aâˆˆaccum = Inhabâ†’âˆˆ accum-prop (Î» n â†’ âˆ£ n , â‰¤â„•-refl , bound n .fst âˆ£â‚)

      xâˆˆaccumâ†’xâ‰¤b : (x : â„) â†’ x âˆˆ accum-sub â†’ x â‰¤ b
      xâˆˆaccumâ†’xâ‰¤b x xâˆˆaccum = Â¬<â†’â‰¥ Â¬x>b
        where
        Â¬x>b : Â¬ x > b
        Â¬x>b x>b =
          proof _ , isPropâŠ¥ by do
          (n , _ , xâ‰¤seqn) â† âˆˆâ†’Inhab accum-prop xâˆˆaccum 0
          return (<â‰¤-asym x>b (â‰¤-trans xâ‰¤seqn (bound n .snd)))

      inhabSub : isInhabited  accum-sub
      inhabSub = âˆ£ a , aâˆˆaccum âˆ£â‚

      boundSub : isUpperBounded  accum-sub
      boundSub = âˆ£ b , xâˆˆaccumâ†’xâ‰¤b âˆ£â‚

    accum-sup : Supremum accum-sub
    accum-sup = getSup (Prop.rec squashâ‚ inhabSub bSeq) (Prop.rec squashâ‚ boundSub bSeq)

    xâ‚€ = accum-sup .sup

    âˆƒfin>xâ‚€ : (Îµ : â„) â†’ Îµ > 0 â†’ âˆ¥ Î£[ nâ‚€ âˆˆ â„• ] ((n : â„•) â†’ nâ‚€ â‰¤â„• n â†’ seq n < xâ‚€ + Îµ) âˆ¥â‚
    âˆƒfin>xâ‚€  Îµ Îµ>0 = do
      (nâ‚€ , Â¬p) â†
        Â¬âˆ€â†’âˆƒÂ¬ (Î» _ â†’ squashâ‚) (âˆ‰â†’Empty accum-prop
          (Â¬âˆˆâ†’âˆ‰ {A = accum-sub} (>supâ†’Â¬âˆˆ _ accum-sup (+-rPosâ†’> Îµ>0))))
      return (nâ‚€ , Î» n nâ‚€â‰¤n â†’ Â¬â‰¤â†’> (Â¬âˆƒâ†’âˆ€Â¬2 Â¬p n nâ‚€â‰¤n))

    âˆƒcluster : isClusteringAt seq xâ‚€
    âˆƒcluster nâ‚€ Îµ Îµ>0 = do
      (mâ‚€ , fin>xâ‚€) â† âˆƒfin>xâ‚€ Îµ Îµ>0
      (x , xâ‚€-Îµ<x , xâˆˆsub) â† <supâ†’âˆƒâˆˆ _ accum-sup (-rPosâ†’< Îµ>0)
      let m = sucmax nâ‚€ mâ‚€
      (n , nâ‰¥m , xâ‰¤seqn) â† âˆˆâ†’Inhab accum-prop xâˆˆsub m
      let xâ‚€-Îµ<seqn : xâ‚€ - Îµ < seq n
          xâ‚€-Îµ<seqn = <â‰¤-trans xâ‚€-Îµ<x xâ‰¤seqn
          seqn<xâ‚€+Îµ : seq n < xâ‚€ + Îµ
          seqn<xâ‚€+Îµ = fin>xâ‚€ n (<-weaken (<â‰¤â„•-trans sucmax>right nâ‰¥m))
      return (n , <â‰¤â„•-trans sucmax>left nâ‰¥m ,
        absInOpenInterval Îµ>0 xâ‚€-Îµ<seqn seqn<xâ‚€+Îµ)


  {-

    Cauchy Sequences in â„

  -}

  -- Cauchy sequence is bounded

  isCauchyâ†’isBoundedSequence : {seq : â„• â†’ â„} â†’ isCauchy seq â†’ isBoundedSequence seq
  isCauchyâ†’isBoundedSequence {seq = seq} cauchy = bSeq
    where

    finBound : (n : â„•) â†’ Î£[ a âˆˆ â„ ] Î£[ b âˆˆ â„ ] ((m : â„•) â†’ (m â‰¤â„• n) â†’ (a â‰¤ seq m) Ã— (seq m â‰¤ b))
    finBound zero = seq 0 , seq 0 , Î» m mâ‰¤n â†’
      subst (Î» k â†’ (seq 0 â‰¤ seq k) Ã— (seq k â‰¤ seq 0)) (sym (â‰¤0â†’â‰¡0 mâ‰¤n)) (â‰¤-refl refl , â‰¤-refl refl)
    finBound (suc n) = a , b , Î» m mâ‰¤n â†’ case-split m (â‰¤-ind mâ‰¤n)
      where
      a' = finBound n .fst
      b' = finBound n .snd .fst
      bfin = finBound n .snd .snd

      a = min a' (seq (suc n))
      b = max b' (seq (suc n))

      case-split : (m : â„•) â†’ (m â‰¤â„• n) âŠ (m â‰¡ suc n) â†’ (a â‰¤ seq m) Ã— (seq m â‰¤ b)
      case-split m (inl mâ‰¤n) = â‰¤-trans minâ‰¤left (bfin _ mâ‰¤n .fst) , â‰¤-trans (bfin _ mâ‰¤n .snd) maxâ‰¥left
      case-split m (inr mâ‰¡sn) = subst (Î» k â†’ (a â‰¤ seq k) Ã— (seq k â‰¤ b)) (sym mâ‰¡sn) (minâ‰¤right , maxâ‰¥right)

    module _
      (Îµ : â„)(Îµ>0 : Îµ > 0)(nâ‚€ : â„•)
      (abs< : (n : â„•) â†’ n >â„• nâ‚€ â†’ abs (seq nâ‚€ - seq n) < Îµ) where

      a = finBound nâ‚€ .fst
      b = finBound nâ‚€ .snd .fst
      bfin = finBound nâ‚€ .snd .snd

      case-split : (n : â„•) â†’ (n >â„• nâ‚€) âŠ (n â‰¤â„• nâ‚€) â†’ (a - Îµ â‰¤ seq n) Ã— (seq n â‰¤ b + Îµ)
      case-split n (inr nâ‰¤nâ‚€) =
        â‰¤-trans (inl (-rPosâ†’< Îµ>0)) (bfin _ nâ‰¤nâ‚€ .fst) ,
        â‰¤-trans (bfin _ nâ‰¤nâ‚€ .snd) (inl (+-rPosâ†’> Îµ>0))
      case-split n (inl n>nâ‚€) =
        inl (absSuppressâ‰¥ (bfin _ â‰¤â„•-refl .fst) (abs< _ n>nâ‚€)) ,
        inl (absSuppressâ‰¤ (bfin _ â‰¤â„•-refl .snd) (abs< _ n>nâ‚€))

      Î£bSeq : Î£[ a âˆˆ â„ ] Î£[ b âˆˆ â„ ] ((n : â„•) â†’ (a â‰¤ seq n) Ã— (seq n â‰¤ b))
      Î£bSeq = a - Îµ , b + Îµ , Î» n â†’ case-split n (<â‰¤-split nâ‚€ n)

    bSeq : isBoundedSequence seq
    bSeq = do
      (nâ‚€ , abs<') â† cauchy 1 1>0
      return (Î£bSeq 1 1>0 (suc nâ‚€) (Î» n n>snâ‚€ â†’
        abs<' (suc nâ‚€) n â‰¤â„•-refl (<â„•-trans â‰¤â„•-refl n>snâ‚€)))


  -- Real Number is Cauchy Complete

  isCauchyâ†’Limit : isCauchyComplete
  isCauchyâ†’Limit {seq = seq} cauchy = record { lim = cluster .point ; conv = converge }
    where

    cluster = isBoundedâ†’ClusterPoint (isCauchyâ†’isBoundedSequence cauchy)

    module _ (Îµ : â„)(Îµ>0 : Îµ > 0) where

      Îµ/2 = middle 0 Îµ
      Îµ/2>0 = middle>l Îµ>0

      converge : âˆ¥ Î£[ nâ‚€ âˆˆ â„• ] ((n : â„•) â†’ n >â„• nâ‚€ â†’ abs (cluster .point - seq n) < Îµ) âˆ¥â‚
      converge = do
        (nâ‚€ , âˆ€abs<) â† cauchy Îµ/2 Îµ/2>0
        (nâ‚ , nâ‚>nâ‚€ , abs<) â† cluster .accum nâ‚€ Îµ/2 Îµ/2>0
        return (
          nâ‚ , Î» n n>nâ‚ â†’ subst (abs (cluster .point - seq n) <_) (x/2+x/2â‰¡x Îµ)
            (â‰¤<-trans (dist-Î” _ _ _) (+-Pres< abs< (âˆ€abs< nâ‚ n nâ‚>nâ‚€ (<â„•-trans nâ‚>nâ‚€ n>nâ‚)))))
