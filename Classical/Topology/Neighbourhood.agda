{-

Neighbourhood

This file contains:
- Basic properties of neighbourhood;
- Basic criterion for open/closed subset;
- Lemmas about separation by open subsets.

-}
{-# OPTIONS --safe #-}
module Classical.Topology.Neighbourhood where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Data.Sigma
open import Cubical.HITs.PropositionalTruncation as Prop

open import Classical.Axioms
open import Classical.Foundations.Powerset

open import Classical.Topology.Base
open import Classical.Topology.Properties

private
  variable
    â„“ : Level


module _ â¦ƒ ğŸ¤– : Oracle â¦„ where

  open Topology


  module _ {X : Type â„“} â¦ƒ ğ’¯ : Topology X â¦„ where

    {-

      Basics

    -}

    -- Neighbourhood around a given point

    â„•bh : X â†’ â„™ â„™ X
    â„•bh x = rep x âˆ© Open

    Nâˆˆâ„•bhxâ†’xâˆˆN : {x : X}{N : â„™ X} â†’ N âˆˆ â„•bh x â†’ x âˆˆ N
    Nâˆˆâ„•bhxâ†’xâˆˆN {x = x} Nâˆˆâ„•x = leftâˆˆ-âˆ© (rep x) Open Nâˆˆâ„•x

    Nâˆˆâ„•bhxâ†’NâˆˆOpen : {x : X}{N : â„™ X} â†’ N âˆˆ â„•bh x â†’ N âˆˆ Open
    Nâˆˆâ„•bhxâ†’NâˆˆOpen {x = x} = rightâˆˆ-âˆ© (rep x) Open

    makeâ„•bh : {x : X}{N : â„™ X} â†’ x âˆˆ N â†’ N âˆˆ Open â†’ N âˆˆ â„•bh x
    makeâ„•bh {x = x} {N = N} xâˆˆN NâˆˆOpen = âˆˆâ†’âˆˆâˆ© (rep x) Open xâˆˆN NâˆˆOpen

    totalâˆˆâ„•bh : {x : X} â†’ total âˆˆ â„•bh x
    totalâˆˆâ„•bh {x = x} = makeâ„•bh {x = x} (xâˆˆtotal {x = x}) (ğ’¯ .has-total)

    â„•bhâˆ© : {x : X}{U V : â„™ X} â†’ U âˆˆ â„•bh x â†’ V âˆˆ â„•bh x â†’ U âˆ© V âˆˆ â„•bh x
    â„•bhâˆ© {U = U} {V = V} Uâˆˆâ„•x Vâˆˆâ„•x =
      makeâ„•bh (âˆ©-âˆˆrep U V (Nâˆˆâ„•bhxâ†’xâˆˆN Uâˆˆâ„•x) (Nâˆˆâ„•bhxâ†’xâˆˆN Vâˆˆâ„•x))
        (ğ’¯ .âˆ©-close (Nâˆˆâ„•bhxâ†’NâˆˆOpen Uâˆˆâ„•x) (Nâˆˆâ„•bhxâ†’NâˆˆOpen Vâˆˆâ„•x))


    {-

      Interior of Subset

    -}


    -- Inside interior of some someset

    _Î£âˆˆâˆ˜_ : (x : X) â†’ (U : â„™ X) â†’ Type _
    x Î£âˆˆâˆ˜ U = Î£[ N âˆˆ â„™ X ] (N âˆˆ â„•bh x) Ã— N âŠ† U


    -- It reads "the point x is inside the interior of U"
    -- or "x is an interior point of U".

    _âˆˆâˆ˜_ : (x : X) â†’ (U : â„™ X) â†’ Type _
    x âˆˆâˆ˜ U = âˆ¥ x Î£âˆˆâˆ˜ U âˆ¥

    âˆˆâ†’âˆˆâˆ˜ : {x : X}{U : â„™ X} â†’ U âˆˆ Open â†’ x âˆˆ U â†’ x âˆˆâˆ˜ U
    âˆˆâ†’âˆˆâˆ˜ {U = U} UâˆˆOpen xâˆˆU = âˆ£ _ , makeâ„•bh xâˆˆU UâˆˆOpen , âŠ†-refl {A = U} âˆ£


    -- A subset U is open,
    -- if every point x âˆˆ U merely has a neighberhood contained in U,

    â„•bhCriterionOfOpenness : {U : â„™ X} â†’ ((x : X) â†’ x âˆˆ U â†’ x âˆˆâˆ˜ U) â†’ U âˆˆ Open
    â„•bhCriterionOfOpenness {U = U} p = UâˆˆOpen
      where
      P : â„™ X â†’ hProp _
      P N = âˆ¥ Î£[ x âˆˆ X ] (N âˆˆ â„•bh x) Ã— N âŠ† U âˆ¥ , squash

      ğ’° : â„™ â„™ X
      ğ’° = specify P

      rec-helper1 : {N : â„™ X} â†’ âˆ¥ Î£[ x âˆˆ X ] (N âˆˆ â„•bh x) Ã— N âŠ† U âˆ¥ â†’ N âˆˆ Open
      rec-helper1 = Prop.rec (isPropâˆˆ Open) Î» (_ , p , _) â†’ Nâˆˆâ„•bhxâ†’NâˆˆOpen p

      ğ’°âŠ†Open : ğ’° âŠ† Open
      ğ’°âŠ†Open p = rec-helper1 (âˆˆâ†’Inhab P p)

      ğ•Œ : â„™ X
      ğ•Œ = union ğ’°

      ğ•ŒâˆˆOpen : ğ•Œ âˆˆ Open
      ğ•ŒâˆˆOpen = ğ’¯ .âˆª-close ğ’°âŠ†Open

      rec-helper2 : {N : â„™ X} â†’ âˆ¥ Î£[ x âˆˆ X ] (N âˆˆ â„•bh x) Ã— N âŠ† U âˆ¥ â†’ N âŠ† U
      rec-helper2 = Prop.rec isPropâŠ† Î» (_ , _ , p) â†’ p

      Nâˆˆğ’°â†’NâŠ†U : (N : â„™ X) â†’ N âˆˆ ğ’° â†’ N âŠ† U
      Nâˆˆğ’°â†’NâŠ†U _ p = rec-helper2 (âˆˆâ†’Inhab P p)

      ğ•ŒâŠ†U : ğ•Œ âŠ† U
      ğ•ŒâŠ†U = unionâŠ† Nâˆˆğ’°â†’NâŠ†U

      UâŠ†ğ•Œ : U âŠ† ğ•Œ
      UâŠ†ğ•Œ xâˆˆU = âˆƒâ†’âˆˆunion
        (Prop.map (Î» (N , Nâˆˆâ„•x , NâŠ†U) â†’ N , Nâˆˆâ„•bhxâ†’xâˆˆN Nâˆˆâ„•x , Inhabâ†’âˆˆ P âˆ£ _ , Nâˆˆâ„•x , NâŠ†U âˆ£) (p _ xâˆˆU))

      ğ•Œâ‰¡U : ğ•Œ â‰¡ U
      ğ•Œâ‰¡U = biâŠ†â†’â‰¡ ğ•ŒâŠ†U UâŠ†ğ•Œ

      UâˆˆOpen : U âˆˆ Open
      UâˆˆOpen = subst (_âˆˆ Open) ğ•Œâ‰¡U ğ•ŒâˆˆOpen


    {-

      Separation by Open Subsets

    -}

    -- Separating a point from a subset using open sets

    Î£Sep : (x : X) â†’ â„™ X â†’ Type _
    Î£Sep x A = Î£[ U âˆˆ â„™ X ] (U âˆˆ â„•bh x) Ã— (A âˆ© U â‰¡ âˆ…)

    Î£SepâŠ† : {x : X}{A B : â„™ X} â†’ A âŠ† B â†’ Î£Sep x B â†’ Î£Sep x A
    Î£SepâŠ† {A = A} {B = B} AâŠ†B (U , Uâˆˆâ„•x , Bâˆ©Uâ‰¡âˆ…) = U , Uâˆˆâ„•x , AâŠ†B+Bâˆ©Câ‰¡âˆ…â†’Aâˆ©Câ‰¡âˆ… AâŠ†B Bâˆ©Uâ‰¡âˆ…


    -- It reads as "there merely exists a neighbourhood of x that is separated from A".

    Sep : (x : X) â†’ â„™ X â†’ Type _
    Sep x A = âˆ¥ Î£Sep x A âˆ¥

    SepâŠ† : {x : X}{A B : â„™ X} â†’ A âŠ† B â†’ Sep x B â†’ Sep x A
    SepâŠ† AâŠ†B = Prop.map (Î£SepâŠ† AâŠ†B)

    Sepâ†’âˆˆâˆ˜âˆ : {x : X}{A : â„™ X} â†’ Sep x A â†’ x âˆˆâˆ˜ (âˆ A)
    Sepâ†’âˆˆâˆ˜âˆ = Prop.map (Î» (U , Uâˆˆâ„•x , Aâˆ©Uâ‰¡âˆ…) â†’ U , Uâˆˆâ„•x , Aâˆ©B=âˆ…â†’AâŠ†âˆB (âˆ©-Comm _ _ âˆ™ Aâˆ©Uâ‰¡âˆ…))


    -- It reads as "there merely exists neighbourhood of x and A respectively that don't intersect with each other",
    -- or "point x and subset A are separating by open sets"

    SepOpen : (x : X) â†’ â„™ X â†’ Type _
    SepOpen x A = âˆ¥ Î£[ U âˆˆ â„™ X ] Î£[ V âˆˆ â„™ X ] (U âˆˆ Open) Ã— A âŠ† U Ã— (V âˆˆ â„•bh x) Ã— (A âˆ© V â‰¡ âˆ…) âˆ¥

    SepOpenâŠ† : {x : X}{A U : â„™ X} â†’ U âˆˆ Open â†’ A âŠ† U â†’ Sep x U â†’ SepOpen x A
    SepOpenâŠ† {U = U} UâˆˆOpen AâŠ†U =
      Prop.map (Î» (V , Vâˆˆâ„•x , Uâˆ©Vâ‰¡âˆ…) â†’ U , V , UâˆˆOpen , AâŠ†U , Vâˆˆâ„•x , AâŠ†B+Bâˆ©Câ‰¡âˆ…â†’Aâˆ©Câ‰¡âˆ… AâŠ†U Uâˆ©Vâ‰¡âˆ…)

    SepOpenâ†’Sep : {x : X}{A : â„™ X} â†’ SepOpen x A â†’ Sep x A
    SepOpenâ†’Sep = Prop.map (Î» (_ , V , _ , _ , Vâˆˆâ„•x , Aâˆ©Vâ‰¡âˆ…) â†’ V , Vâˆˆâ„•x , Aâˆ©Vâ‰¡âˆ…)


    -- A subset K âŠ† X is closed if for any x âˆ‰ K, there merely exists neigubourhood of x separating from K.

    SepCriterionOfClosedness : {K : â„™ X} â†’ ((x : X) â†’ x âˆ‰ K â†’ Sep x K) â†’ K âˆˆ Closed
    SepCriterionOfClosedness {K = K} sep = â„•bhCriterionOfOpenness xâˆ‰Kâ†’xâˆˆâˆ˜âˆK
      where
      xâˆ‰Kâ†’xâˆˆâˆ˜âˆK : (x : X) â†’ x âˆˆ (âˆ K) â†’ x âˆˆâˆ˜ (âˆ K)
      xâˆ‰Kâ†’xâˆˆâˆ˜âˆK x xâˆˆâˆK = Sepâ†’âˆˆâˆ˜âˆ (sep x (âˆˆâˆâ†’âˆ‰ {A = K} xâˆˆâˆK))


    -- Given a finite covering ğ’° such that,
    -- for any open U âˆˆ ğ’°, there merely exists a neighbourhood of x outside U,
    -- then there merely exists a neighbourhood of x that does not intersect with the union of opens in ğ’°.

    unionSep : (x : X)
      (ğ’° : â„™ â„™ X)(ğ’°âŠ†Open : ğ’° âŠ† Open)
      (sep : (U : â„™ X) â†’ U âˆˆ ğ’° â†’ Sep x U)
      â†’ isFinSub ğ’° â†’ Sep x (union ğ’°)
    unionSep _ _ ğ’°âŠ†Open sep (fin-squash p q i) = squash (unionSep _ _ ğ’°âŠ†Open sep p) (unionSep _ _ ğ’°âŠ†Open sep q) i
    unionSep x ğ’° _ _ isfinâˆ… = âˆ£ total , totalâˆˆâ„•bh {x = x} , âˆ©-rUnit (union ğ’°) âˆ™ unionâˆ… âˆ£
    unionSep x ğ’° ğ’°âŠ†Open sep (isfinsuc {A = ğ’°â‚€} finğ’°â‚€ U) = subst (Sep x) (sym unionâˆª[A]) sepğ•Œâ‚€âˆªU
      where
      ğ•Œâ‚€ : â„™ X
      ğ•Œâ‚€ = union ğ’°â‚€

      ğ’°â‚€âŠ†ğ’° : ğ’°â‚€ âŠ† ğ’°
      ğ’°â‚€âŠ†ğ’° = âˆª-leftâŠ† ğ’°â‚€ [ U ]

      ğ’°â‚€âŠ†Open : ğ’°â‚€ âŠ† Open
      ğ’°â‚€âŠ†Open = âŠ†-trans {A = ğ’°â‚€} ğ’°â‚€âŠ†ğ’° ğ’°âŠ†Open

      ğ•Œâ‚€âˆˆOpen : ğ•Œâ‚€ âˆˆ Open
      ğ•Œâ‚€âˆˆOpen = unionâˆˆOpen ğ’°â‚€âŠ†Open

      -- TODO : Make a solver to deal with these problems.
      âˆªâˆ…-helper : {A B C D : â„™ X} â†’ A âˆ© C â‰¡ âˆ… â†’ B âˆ© D â‰¡ âˆ… â†’ (A âˆª B) âˆ© (C âˆ© D) â‰¡ âˆ…
      âˆªâˆ…-helper {A = A} {B = B} {C = C} {D = D} Aâˆ©Câ‰¡âˆ… Bâˆ©Dâ‰¡âˆ… =
          âˆ©-âˆª-lDist _ _ _
        âˆ™ (Î» i â†’ âˆ©-Assoc A C D i âˆª (B âˆ© âˆ©-Comm C D i))
        âˆ™ (Î» i â†’ ((A âˆ© C) âˆ© D) âˆª âˆ©-Assoc B D C i)
        âˆ™ (Î» i â†’ (Aâˆ©Câ‰¡âˆ… i âˆ© D) âˆª (Bâˆ©Dâ‰¡âˆ… i âˆ© C))
        âˆ™ (Î» i â†’ âˆ©-lZero D i âˆª âˆ©-lZero C i)
        âˆ™ âˆª-Idem _

      ind-Sep-helper : (A B : â„™ X) â†’ A âˆˆ Open â†’ B âˆˆ Open â†’ Î£Sep x A â†’ Î£Sep x B â†’ Î£Sep x (A âˆª B)
      ind-Sep-helper _ _ _ _ (VA , VAâˆˆNx , VAâˆ…) (VB , VBâˆˆNx , VBâˆ…) =
        VA âˆ© VB , â„•bhâˆ© VAâˆˆNx VBâˆˆNx , âˆªâˆ…-helper VAâˆ… VBâˆ…

      ind-Sep : (A B : â„™ X) â†’ A âˆˆ Open â†’ B âˆˆ Open â†’ _
      ind-Sep A B p q = Prop.map2 (ind-Sep-helper A B p q)

      sepğ•Œâ‚€ : Sep x ğ•Œâ‚€
      sepğ•Œâ‚€ = unionSep _ _ ğ’°â‚€âŠ†Open (Î» U Uâˆˆğ’°â‚€ â†’ sep U (âˆˆâŠ†-trans {A = ğ’°â‚€} Uâˆˆğ’°â‚€ ğ’°â‚€âŠ†ğ’°)) finğ’°â‚€

      Uâˆˆğ’° : U âˆˆ ğ’°
      Uâˆˆğ’° = [A]âŠ†Sâ†’AâˆˆS (âˆª-rightâŠ† ğ’°â‚€ [ U ])

      UâˆˆOpen : U âˆˆ Open
      UâˆˆOpen = âˆˆâŠ†-trans {A = ğ’°} Uâˆˆğ’° ğ’°âŠ†Open

      sep[U] : Sep x U
      sep[U] = sep U Uâˆˆğ’°

      sepğ•Œâ‚€âˆªU : Sep x (ğ•Œâ‚€ âˆª U)
      sepğ•Œâ‚€âˆªU = ind-Sep _ _ ğ•Œâ‚€âˆˆOpen UâˆˆOpen sepğ•Œâ‚€ sep[U]
